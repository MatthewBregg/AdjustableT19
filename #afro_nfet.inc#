;**** **** **** **** ****
;
;die benutzung der software ist mit folgenden bedingungen verbunden:
;
;1. da ich alles kostenlos zur verfügung stelle, gebe ich keinerlei garantie
;   und übernehme auch keinerlei haftung für die folgen der benutzung.
;
;2. die software ist ausschließlich zur privaten nutzung bestimmt. ich
;   habe nicht geprüft, ob bei gewerblicher nutzung irgendwelche patentrechte
;   verletzt werden oder sonstige rechtliche einschränkungen vorliegen.
;
;3. jeder darf änderungen vornehmen, z.b. um die funktion seinen bedürfnissen
;   anzupassen oder zu erweitern. ich würde mich freuen, wenn ich weiterhin als
;   co-autor in den unterlagen erscheine und mir ein link zur entprechenden seite
;   (falls vorhanden) mitgeteilt wird.
;
;4. auch nach den änderungen sollen die software weiterhin frei sein, d.h. kostenlos bleiben.
;
;!! wer mit den nutzungbedingungen nicht einverstanden ist, darf die software nicht nutzen !!
;
; tp-18a
; october 2004
; autor: bernhard konze
; email: bernhard.konze@versanet.de
;--
; based on upon bernhard's "tp-18a" and others; see
; http://home.versanet.de/~b-konze/blc_18a/blc_18a.htm
; copyright (c) 2004 bernhard konze
; copyright (c) 2011-2012 simon kirby and other contributors
; no warranty expressed or implied. use at your own risk. always test
; without propellers! please respect bernhard konze's license above.
;--
; warning: i have blown fets on turnigy plush 18a escs in previous versions
; of this code with my modifications. some bugs have since been fixed, such
; as leaving pwm enabled while busy-looping forever outside of isr code.
; however, this does run with higher pwm frequency than most original code,
; so higher fet temperatures may occur! use at your own risk, and maybe see
; how it compares and let me know!
;
; warning: this does not check temperature or voltage adc inputs.
;
; note: we do 16-bit pwm on timer2 at full cpu clock rate resolution, using
; tcnt2h to simulate the high byte. an input full to stop range of 800 plus
; a min_duty of 56 (a power_range of 856) gives 800 unique pwm steps at an
; about 18khz on a 16mhz cpu clock. the output frequency is slightly lower
; than f_cpu / power_range due to cycles used in the interrupt as tcnt2 is
; reloaded.
;
; simon kirby <sim@simulated.ca>
;
;-- device ----------------------------------------------------------------
;
.include "m8def.inc"
;
; 8k bytes of in-system self-programmable flash
; 512 bytes eeprom
; 1k byte internal sram
;
;-- fuses -----------------------------------------------------------------
;
; old fuses for internal rc oscillator at 8mhz were lfuse=0xa4 hfuse=0xdf,
; but since we now set osccal to 0xff (about 16mhz), running under 4.5v is
; officially out of spec. we'd better set the brown-out detection to 4.0v.
; the resulting code works with or without external 16mhz oscillators.
; boards with external oscillators can use lfuse=0x3f.
;
; if the boot loader is enabled, the last nibble of the hfuse should be set
; to 'a' or '2' to also enable eesave - save eeprom on chip erase. this is
; a 512-word boot flash section (0xe00), and enable bootrst to jump to it.
; setting these fuses actually has no harm even without the boot loader,
; since 0xffff is nop, and it will just nop-sled around into normal code.
;
; suggested fuses with 4.0v brown-out voltage:
; without external oscillator: avrdude -u lfuse:w:0x24:m -u hfuse:w:0xda:m
;    with external oscillator: avrdude -u lfuse:w:0x3f:m -u hfuse:w:0xca:m
;
; don't set wdton if using the boot loader. we will enable it on start.
;
;-- board -----------------------------------------------------------------
;
; the following only works with avra or avrasm2.
; for avrasm32, just comment out all but the include you need.
#if defined(afro_esc)
#include "afro.inc"		; afroesc (icp pwm, i2c, uart)
#elif defined(afro2_esc)
#include "afro2.inc"		; afroesc 2 (icp pwm, i2c, uart)
#elif defined(afro_hv_esc)
#include "afro_hv.inc"		; afroesc hv with drivers (icp pwm, i2c, uart)
#elif defined(afro_nfet_esc)
#include "afro_nfet.inc"	; afroesc 3 with all nfets (icp pwm, i2c, uart)
#elif defined(afro_pr0_esc)
#include "afro_pr0.inc"		; afroesc prototype rev0 with ncp5911 (icp pwm)
#elif defined(afro_pr1_esc)
#include "afro_pr1.inc"		; afroesc prototype rev1 with ncp5911 (icp pwm)
#elif defined(arctictiger_esc)
#include "arctictiger.inc"	; arctic tiger 30a esc with all nfets (icp pwm)
#elif defined(birdie70a_esc)
#include "birdie70a.inc"	; birdie 70a with all nfets (int0 pwm)
#elif defined(mkblctrl1_esc)
#include "mkblctrl1.inc"	; mk bl-ctrl v1.2 (icp pwm, i2c, uart, high side pwm, sense hack)
#elif defined(bs_esc)
#include "bs.inc"		; hobbyking blueseries / mystery (int0 pwm)
#elif defined(bs_nfet_esc)
#include "bs_nfet.inc"		; hobbyking blueseries / mystery with all nfets (int0 pwm)
#elif defined(bs40a_esc)
#include "bs40a.inc"		; hobbyking blueseries / mystery 40a (int0 pwm)
#elif defined(dlu40a_esc)
#include "dlu40a.inc"		; pulso advance plus 40a dlu40a inverted-pwm-opto (int0 pwm)
#elif defined(dlux_esc)
#include "dlux.inc"		; hobbyking dlux turnigy esc 20a
#elif defined(diy0_esc)
#include "diy0.inc"		; hobbyking diy open esc (unreleased rev 0)
#elif defined(dys_nfet_esc)
#include "dys_nfet.inc"		; dys 30a esc with all nfets (icp pwm, i2c, uart)
#elif defined(hk200a_esc)
#include "hk200a.inc"		; hobbyking ss series 190-200a with all nfets (int0 pwm)
#elif defined(hm135a_esc)
#include "hm135a.inc"		; hacker/jeti master 135-o-f5b 135a inverted-pwm-opto (int0 pwm)
#elif defined(hxt200a_esc)
#include "hxt200a.inc"		; hextronik f3j hxt200a hv esc (int0 pwm, i2c, uart)
#elif defined(kda_esc)
#include "kda.inc"		; keda/multistar 12a, 20a, 30a (original) (inverted int0 pwm)
#elif defined(kda_8khz_esc)
#include "kda_8khz.inc"		; keda/multistar 30a (early 2014) (inverted int0 pwm)
#elif defined(kda_nfet_esc)
#include "kda_nfet.inc"		; keda/multistar 30a with all nfets (inverted int0 pwm)
#elif defined(kda_nfet_ni_esc)
#include "kda_nfet_ni.inc"	; keda/multistar/sunrise ~30a with all nfets (int0 pwm)
#elif defined(rb50a_esc)
#include "rb50a.inc"		; red brick 50a with all nfets (int0 pwm)
#elif defined(rb70a_esc)
#include "rb70a.inc"		; red brick 70a with all nfets (int0 pwm)
#elif defined(rb70a2_esc)
#include "rb70a2.inc"		; newer red brick 70a with blue pcb and all nfets (int0 pwm)
#elif defined(rct50a_esc)
#include "rct50a.inc"		; rctimer 50a (mlf version) with all nfets (int0 pwm)
#elif defined(tbs_esc)
#include "tbs.inc"		; tbs 30a esc (team blacksheep) with all nfets (icp pwm, uart)
#elif defined(tbs_hv_esc)
#include "tbs_hv.inc"		; tbs high voltage esc (team blacksheep) with all nfets (icp pwm, uart)
#elif defined(tp_esc)
#include "tp.inc"		; towerpro 25a/hobbyking 18a "type 1" (int0 pwm)
#elif defined(tp_8khz_esc)
#include "tp_8khz.inc"		; towerpro 25a/hobbyking 18a "type 1" (int0 pwm) at 8khz pwm
#elif defined(tp_i2c_esc)
#include "tp_i2c.inc"		; towerpro 25a/hobbyking 18a "type 1" (i2c)
#elif defined(tp_nfet_esc)
#include "tp_nfet.inc"		; towerpro 25a with all nfets "type 3" (int0 pwm)
#elif defined(tp70a_esc)
#include "tp70a.inc"		; towerpro 70a with bl8003 fet drivers (int0 pwm)
#elif defined(tgy6a_esc)
#include "tgy6a.inc"		; turnigy plush 6a (int0 pwm)
#elif defined(tgy_8mhz_esc)
#include "tgy_8mhz.inc"		; towerpro/turnigy basic/plush "type 2" w/8mhz oscillator (int0 pwm)
#elif defined(tgy_esc)
#include "tgy.inc"		; towerpro/turnigy basic/plush "type 2" (int0 pwm)
#else
#error "unrecognized board type."
#endif

.equ	cpu_mhz		= f_cpu / 1000000

.equ	boot_loader	= 1	; include turnigy usb linker stk500v2 boot loader on pwm input pin
.equ	boot_jump	= 1	; jump to any boot loader when pwm input stays high
.equ	boot_start	= thirdbootstart

.if !defined(high_side_pwm)
.equ	high_side_pwm	= 0	; experimental - torukmakto4 04-30-18
.endif

.if !defined(comp_pwm)
.equ	comp_pwm	= 0	; during pwm off, switch high side on (unsafe on some boards!)
.endif
.if !defined(dead_low_ns)
.equ	dead_low_ns	= 1000	; low-side dead time w/comp_pwm (62.5ns steps @ 16mhz, max 2437ns)
.equ	dead_high_ns	= 1000	; high-side dead time w/comp_pwm (62.5ns steps @ 16mhz, max roughly pwm period)
.endif
.equ	dead_time_low	= dead_low_ns * cpu_mhz / 1000
.equ	dead_time_high	= dead_high_ns * cpu_mhz / 1000

.if !defined(motor_advance)
.equ	motor_advance	= 13	; degrees of timing advance (0 - 30, 30 meaning no delay)
.endif
.if !defined(timing_offset)
.equ	timing_offset	= 0	; motor timing offset in microseconds
.endif
.equ	motor_brake	= 0	; enable brake during neutral/idle ("motor drag" brake)
.equ	low_brake	= 0	; enable brake on very short rc pulse ("thumb" brake like on airtronics xl2p)
.if !defined(motor_reverse)
.equ	motor_reverse	= 0	; reverse normal commutation direction
.endif
.equ	rc_puls_reverse	= 0	; enable rc-car style forward/reverse throttle
.equ	rc_calibration	= 0	; support run-time calibration of min/max pulse lengths
.equ	slow_throttle	= 1	; limit maximum throttle jump to try to prevent overcurrent
.equ	beacon		= 1	; beep periodically when rc signal is lost
.equ	beacon_idle	= 0	; beep periodically if idle for a long period
.if !defined(check_hardware)
.equ	check_hardware	= 0	; check for correct pin configuration, sense inputs, and functioning mosfets
.endif
.equ	cell_max_dv	= 43	; maximum battery cell deciv
.equ	cell_min_dv	= 35	; minimum battery cell deciv
.equ	cell_count	= 0	; 0: auto, >0: hard-coded number of cells (for reliable lvc > ~4s)
.equ	blip_cell_count	= 0	; blip out cell count before arming
.equ	debug_adc_dump	= 0	; output an endless loop of all adc values (no normal operation)
.equ	motor_debug	= 0	; output sync pulses on mosi or sck, debug flag on miso

.equ	i2c_addr	= 0x50	; mk-style i2c address
.equ	motor_id	= 1	; mk-style i2c motor id, or uart motor number

.equ	rcp_tot		= 2	; number of 65536us periods before considering rc pulse lost

; these are now defaults which can be adjusted via throttle calibration
; (stick high, stick low, (stick neutral) at start).
; these might be a bit wide for most radios, but lines up with power_range.
.equ	stop_rc_puls	= 1060	; stop motor at or below this pulse length
.equ	full_rc_puls	= 1560	; full speed at or above this pulse length
.equ	max_rc_puls	= 2400	; throw away any pulses longer than this
.equ	min_rc_puls	= 768	; throw away any pulses shorter than this
.equ	mid_rc_puls	= (stop_rc_puls + full_rc_puls) / 2	; neutral when rc_puls_reverse = 1
.equ	rcp_alias_shift	= 3	; enable 1/8th pwm input alias ("oneshot125")
.equ	beep_rcp_error	= 0	; beep at stop if invalid pwm pulses were received

.if	rc_puls_reverse
.equ	rcp_deadband	= 50	; do not start until this much above or below neutral
.equ	program_rc_puls	= (stop_rc_puls + full_rc_puls * 3) / 4	; normally 1660
.else
.equ	rcp_deadband	= 0
.equ	program_rc_puls	= (stop_rc_puls + full_rc_puls) / 2	; normally 1460
.endif

.if	low_brake
.equ	rcp_low_dband	= 60	; brake at this many microseconds below low pulse
.endif

.equ	max_drift_puls	= 10	; maximum jitter/drift microseconds during programming

; minimum pwm on-time (too low and fets won't turn on, hard starting)
.if !defined(min_duty)
.equ	min_duty	= 56 * cpu_mhz / 16
.endif

; number of pwm steps (too high and pwm frequency drops into audible range)
.if !defined(power_range)
.equ	power_range	= 800 * cpu_mhz / 16 + min_duty
.endif

.equ	max_power	= (power_range-1)
.equ	pwr_cool_start	= (power_range/24) ; power limit while starting to reduce heating
.equ	pwr_min_start	= (power_range/6) ; power limit while starting (to start)
.equ	pwr_max_start	= (power_range/6) ; power limit while starting (if still not running)
.equ	pwr_max_rpm1	= (power_range/6) ; power limit when running slower than timing_range1
.equ	pwr_max_rpm2	= (power_range/2) ; power limit when running slower than timing_range2 (note this is unused and for legacy "powershift" versions --tm4)

.equ	brake_power	= max_power*2/3	; brake force is exponential, so start fairly high
.equ	brake_speed	= 3		; speed to reach max_power, 0 (slowest) - 8 (fastest)
.equ	low_brake_power	= max_power*2/3
.equ	low_brake_speed	= 5

.equ	timing_min	= 0x8000 ; 8192us per commutation
.equ	timing_range1	= 0x6000 ; tm4 change - start ramping duty earlier due to less pwr_max_rpm1 (stock 0x4000 - 4096us per commutation)
.equ	timing_range2	= 0x2000 ; 2048us per commutation (note this is unused and for legacy "powershift" versions --tm4)
.equ	timing_range3	= 0x1000 ; 1024us per commutation
.equ	timing_max	= 0x0103 ; use that as governor setpoint (e0=25510rpm hy-con 9.5mm -- torukmakto4 04-28-18)

.equ	timeout_start	= 48000	; timeout per commutation for zc during starting
.if !defined(start_delay_us)
.equ	start_delay_us	= 0	; initial post-commutation wait during starting
.endif
.equ	start_dstep_us	= 8	; microseconds per start delay step
.equ	start_delay_inc	= 15	; wait step count increase (wraps in a byte)
.equ	start_mod_inc	= 4	; start power modulation step count increase (wraps in a byte)
.equ	start_mod_limit	= 48	; value at which power is reduced to avoid overheating
.equ	start_fail_inc	= 16	; start_tries step count increase (wraps in a byte, upon which we disarm)

.equ	enough_goodies	= 6	; this many start cycles without timeout will transition to running mode (tm4 experimental 05-01-18 - stock 12)
.equ	zc_check_fast	= 12	; number of zc check loops under which pwm noise should not matter
.equ	zc_check_max	= power_range / 32 ; limit zc checking to about 1/2 pwm interval
.equ	zc_check_min	= 3

.equ	t0clk		= (1<<cs01)	; clk/8 == 2mhz
.equ	t1clk		= (1<<cs10)+(use_icp<<ices1)+(use_icp<<icnc1)	; clk/1 == 16mhz
.equ	t2clk		= (1<<cs20)	; clk/1 == 16mhz

.equ	eeprom_sign	= 31337		; random 16-bit value
.equ	eeprom_offset	= 0x80		; offset into 512-byte space (why not)

; conditional code inclusion
.set	debug_tx	= 0		; output debugging on uart tx pin
.set	adc_read_needed	= 0		; reading from adcs

;**** **** **** **** ****
; register definitions
.def	temp5		= r0		; aux temporary (l) (limited operations)
.def	temp6		= r1		; aux temporary (h) (limited operations)
.def	duty_l		= r2		; on duty cycle low, one's complement
.def	duty_h		= r3		; on duty cycle high
.def	off_duty_l	= r4		; off duty cycle low, one's complement
.def	off_duty_h	= r5		; off duty cycle high
.def	rx_l		= r6		; received throttle low
.def	rx_h		= r7		; received throttle high
.def	tcnt2h		= r8		; timer2 high byte
.def	i_sreg		= r9		; status register save in interrupts
.def	temp7		= r10		; really aux temporary (limited operations)
.def	rc_timeout	= r11
.def	sys_control_l	= r12		; duty limit low (word register aligned)
.def	sys_control_h	= r13		; duty limit high
.def	timing_duty_l	= r14		; timing duty limit low
.def	timing_duty_h	= r15		; timing duty limit high
.def	flags0		= r16	; state flags
	.equ	oct1_pending	= 0	; if set, output compare interrupt is pending
	.equ	set_duty	= 1	; if set when armed, set duty during evaluate_rc
	.equ	rcp_error	= 2	; if set, corrupted pwm input was seen
	.equ	rcp_alias	= 3	; if set, rc alias (shifted) range is active
	.equ	eeprom_reset	= 4	; if set, reset eeprom
	.equ	eeprom_write	= 5	; if set, save settings to eeprom
	.equ	uart_sync	= 6	; if set, we are waiting for our serial throttle byte
	.equ	no_calibration	= 7	; if set, disallow calibration (unsafe reset cause)
.def	flags1		= r17	; state flags
	.equ	power_on	= 0	; if set, switching fets is enabled
	.equ	full_power	= 1	; 100% on - don't switch off, but do off_cycle working
	.equ	i2c_mode	= 2	; if receiving updates via i2c
	.equ	uart_mode	= 3	; if receiving updates via uart
	.equ	eval_rc		= 4	; if set, evaluate rc command while waiting for oct1
	.equ	aco_edge_high	= 5	; if set, looking for aco high - same bit position as aco
	.equ	startup		= 6	; if set, startup-phase is active
	.equ	reverse		= 7	; if set, do reverse commutation
.def	flags2		= r18
	.equ	a_fet		= 0	; if set, a fet is being pwmed
	.equ	b_fet		= 1	; if set, b fet is being pwmed
	.equ	c_fet		= 2	; if set, c fet is being pwmed
	.equ	all_fets	= (1<<a_fet)+(1<<b_fet)+(1<<c_fet)
	.equ	timing_fast	= 6	; if set, timing fits in 16 bits
	.equ	skip_cpwm	= 7	; if set, skip complementary pwm (for short off period)
;.def			= r19
.def	i_temp1		= r20		; interrupt temporary
.def	i_temp2		= r21		; interrupt temporary
.def	temp3		= r22		; main temporary (l)
.def	temp4		= r23		; main temporary (h)
.def	temp1		= r24		; main temporary (l), adiw-capable
.def	temp2		= r25		; main temporary (h), adiw-capable

; xl: general temporary
; xh: general temporary
; yl: general temporary
; yh: general temporary
; zl: next pwm interrupt vector (low)
; zh: next pwm interrupt vector (high, stays at zero) -- used as "zero" register

;**** **** **** **** ****
; ram definitions
.dseg				; data segment
.org sram_start

orig_osccal:	.byte	1	; original osccal value
goodies:	.byte	1	; number of rounds without timeout
powerskip:	.byte	1	; skip power through this number of steps
ocr1ax:		.byte	1	; 3rd byte of ocr1a
tcnt1x:		.byte	1	; 3rd byte of tcnt1
pwm_on_ptr:	.byte	1	; next pwm on vector
rct_boot:	.byte	1	; counter which increments while rc_timeout is 0 to jump to boot loader
rct_beacon:	.byte	1	; counter which increments while rc_timeout is 0 to disarm and beep occasionally
idle_beacon:	.byte	1	; counter which increments while armed and idle to beep occasionally
last_tcnt1_l:	.byte	1	; last timer1 value
last_tcnt1_h:	.byte	1
last_tcnt1_x:	.byte	1
l2_tcnt1_l:	.byte	1	; last last timer1 value
l2_tcnt1_h:	.byte	1
l2_tcnt1_x:	.byte	1
timing_l:	.byte	1	; interval of 2 commutations
timing_h:	.byte	1
timing_x:	.byte	1
com_time_l:	.byte	1	; time of last commutation
com_time_h:	.byte	1
com_time_x:	.byte	1
start_delay:	.byte	1	; delay count after starting commutations before checking back-emf
start_modulate:	.byte	1	; start modulation counter (to reduce heating from pwr_max_start if stuck)
start_fail:	.byte   1	; number of start_modulate loops for eventual failure and disarm
rcp_beep_count:	.byte	1	; number of rc pulse error beeps
rc_duty_l:	.byte	1	; desired duty cycle
rc_duty_h:	.byte	1
fwd_scale_l:	.byte	1	; 16.16 multipliers to scale input rc pulse to power_range
fwd_scale_h:	.byte	1
rev_scale_l:	.byte	1
rev_scale_h:	.byte	1
neutral_l:	.byte	1	; offset for neutral throttle (in cpu_mhz)
neutral_h:	.byte	1
.if rcp_deadband && defined(rcp_alias_shift)
deadband_l:	.byte	1	; deadband scaled for possible input alias
deadband_h:	.byte	1
.endif
.if low_brake
low_brake_l:	.byte	1	; low brake position with deadband applied
low_brake_h:	.byte	1
.endif
.if use_i2c
i2c_max_pwm:	.byte	1	; maxpwm for mk (note: 250 while stopped is magic and enables v2)
i2c_rx_state:	.byte	1
i2c_blc_offset:	.byte	1
.endif
motor_count:	.byte	1	; motor number for serial control
brake_sub:	.byte	1	; brake speed subtrahend (power of two)
brake_want:	.byte	1	; type of brake desired
brake_active:	.byte	1	; type of brake active
;**** **** **** **** ****
; the following entries are block-copied from/to eeprom
eeprom_sig_l:	.byte	1
eeprom_sig_h:	.byte	1
puls_high_l:	.byte	1	; -,
puls_high_h:	.byte	1	;  |
puls_low_l:	.byte	1	;  |- saved pulse lengths during throttle calibration
puls_low_h:	.byte	1	;  |  (order used by rc_prog)
puls_neutral_l:	.byte	1	;  |
puls_neutral_h:	.byte	1	; -'
.if use_i2c
blc_revision:	.byte	1	; blconfig revision
blc_setmask:	.byte	1	; blconfig settings mask
blc_pwmscaling:	.byte	1	; blconfig pwm scaling
blc_currlimit:	.byte	1	; blconfig current limit
blc_templimit:	.byte	1	; blconfig temperature limit
blc_currscale:	.byte	1	; blconfig current scaling
blc_bitconfig:	.byte	1	; blconfig bitconfig (1 == motor_reverse)
blc_checksum:	.byte	1	; blconfig checksum (0xaa + above bytes)
.endif
eeprom_end:	.byte	1
;-----bko-----------------------------------------------------------------
;**** **** **** **** ****
.cseg
.org 0
;**** **** **** **** ****
; atmega8 interrupts

;.equ	int0addr=$001	; external interrupt0 vector address
;.equ	int1addr=$002	; external interrupt1 vector address
;.equ	oc2addr =$003	; output compare2 interrupt vector address
;.equ	ovf2addr=$004	; overflow2 interrupt vector address
;.equ	icp1addr=$005	; input capture1 interrupt vector address
;.equ	oc1aaddr=$006	; output compare1a interrupt vector address
;.equ	oc1baddr=$007	; output compare1b interrupt vector address
;.equ	ovf1addr=$008	; overflow1 interrupt vector address
;.equ	ovf0addr=$009	; overflow0 interrupt vector address
;.equ	spiaddr =$00a	; spi interrupt vector address
;.equ	urxcaddr=$00b	; usart receive complete interrupt vector address
;.equ	udreaddr=$00c	; usart data register empty interrupt vector address
;.equ	utxcaddr=$00d	; usart transmit complete interrupt vector address
;.equ	adccaddr=$00e	; adc interrupt vector address
;.equ	erdyaddr=$00f	; eeprom interrupt vector address
;.equ	aciaddr =$010	; analog comparator interrupt vector address
;.equ	twiaddr =$011	; irq. vector address for two-wire interface
;.equ	spmaddr =$012	; spm complete interrupt vector address
;.equ	spmraddr =$012	; spm complete interrupt vector address

;-----bko-----------------------------------------------------------------
; reset and interrupt jump table
; when multiple interrupts are pending, the vectors are executed from top
; (ext_int0) to bottom.
		rjmp reset	; reset
		rjmp rcp_int	; ext_int0
		reti		; ext_int1
		reti		; t2oc_int
		ijmp		; t2ovfl_int
		rjmp rcp_int	; icp1_int
		rjmp t1oca_int	; t1oca_int
		reti		; t1ocb_int
		rjmp t1ovfl_int	; t1ovfl_int
		reti		; t0ovfl_int
		reti		; spi_int
		rjmp urxc_int	; urxc
		reti		; udre
		reti		; utxc
		reti		; adc_int
		reti		; eep_int
		reti		; aci_int
		rjmp i2c_int	; twi_int
		reti		; spmc_int

eeprom_defaults_w:
	.db low(eeprom_sign), high(eeprom_sign)
	.db byte1(full_rc_puls * cpu_mhz), byte2(full_rc_puls * cpu_mhz)
	.db byte1(stop_rc_puls * cpu_mhz), byte2(stop_rc_puls * cpu_mhz)
	.db byte1(mid_rc_puls * cpu_mhz), byte2(mid_rc_puls * cpu_mhz)
.if use_i2c
.equ	bl_revision	= 2
	.db bl_revision, 144	; revision, setmask -- settings mask should encode motor_reverse bit
	.db 255, 255		; pwmscaling, currentlimit
	.db 127, 0		; templimit, currentscaling
	.db 0, byte1(0xaa + bl_revision + 144 + 255 + 255 + 127 + 0 + 0)	; bitconfig, crc (0xaa + sum of above bytes)
.endif

;-- instruction extension macros -----------------------------------------

; add any 16-bit immediate to a register pair (@0:@1 += @2), no z flag
.macro adi2
	.if byte1(-@2)
		subi	@0, byte1(-@2)
		sbci	@1, byte1(-byte2(@2 + 0xff))
	.else
		subi	@1, byte1(-byte2(@2 + 0xff))
	.endif
.endmacro

; smaller version for r24 and above, z flag not reliable
.macro adiwx
	.if !(@2 & 0xff) || (@2) & ~0x3f
		adi2	@0, @1, @2
	.else
		adiw	@0, @2
	.endif
.endmacro

; compare any 16-bit immediate from a register pair (@0:@1 -= @2, maybe clobbering @3)
.macro cpiz2
		cpi	@0, byte1(@2)
	.if byte2(@2)
		ldi	@3, byte2(@2)
		cpc	@1, @3
	.else
		cpc	@1, zh
	.endif
.endmacro

; compare any 16-bit immediate from a register pair (@0:@1 -= @2, maybe clobbering @3), no z flag
; do not follow by z flag tests like breq, brne, brlt, brge, brlo, brsh!
; the idea here is that the low byte being compared with (subtracted by)
; 0 will never set carry, so skipping it and cpi'ing the high byte is the
; same other than the result of the z flag.
.macro cpi2
	.if byte1(@2)
		cpiz2	@0, @1, @2, @3
	.else
		cpi	@1, byte2(@2)
	.endif
.endmacro

; compare any 24-bit immediate from a register triplet (@0:@1:@2 -= @3, maybe clobbering @4)
.macro cpiz3
		cpi	@0, byte1(@3)
	.if byte2(@3)
		ldi	@4, byte2(@3)
		cpc	@1, @4
	.else
		cpc	@1, zh
	.endif
	.if byte3(@3)
		ldi	@4, byte3(@3)
		cpc	@2, @4
	.else
		cpc	@2, zh
	.endif
.endmacro

; compare any 24-bit immediate from a register triplet (@0:@1:@2 -= @3, maybe clobbering @4)
; may not set z flag, as above.
.macro cpi3
	.if byte1(@3)
		cpiz3	@0, @1, @2, @3, @4
	.else
		cpi2	@1, @2, @3 >> 8, @4
	.endif
.endmacro

; subtract any 16-bit immediate from a register pair (@0:@1 -= @2), no z flag
.macro sbi2
	.if byte1(@2)
		subi	@0, byte1(@2)
		sbci	@1, byte2(@2)
	.else
		subi	@1, byte2(@2)
	.endif
.endmacro

; smaller version for r24 and above, z flag not reliable
.macro sbiwx
	.if !(@2 & 0xff) || (@2) & ~0x3f
		sbi2	@0, @1, @2
	.else
		sbiw	@0, @2
	.endif
.endmacro

; load 2-byte immediate
.macro ldi2
		ldi	@0, byte1(@2)
		ldi	@1, byte2(@2)
.endmacro

; load 3-byte immediate
.macro ldi3
		ldi	@0, byte1(@3)
		ldi	@1, byte2(@3)
		ldi	@2, byte3(@3)
.endmacro

; register out to any address (memory-mapped if necessary)
.macro outr
	.if @0 < 64
		out	@0, @1
	.else
		sts	@0, @1
	.endif
.endmacro

; register in from any address (memory-mapped if necessary)
.macro inr
	.if @1 < 64
		in	@0, @1
	.else
		lds	@0, @1
	.endif
.endmacro

; immediate out to any port (possibly via @2 as a temporary)
.macro outi
	.if @1
		ldi	@2, @1
		outr	@0, @2
	.else
		outr	@0, zh
	.endif
.endmacro

;-- led macros -----------------------------------------------------------

.if !defined(red_led)
	.macro red_on
	.endmacro
	.macro red_off
	.endmacro
.endif

.if !defined(green_led)
	.macro grn_on
	.endmacro
	.macro grn_off
	.endmacro
.endif

.if !defined(blue_led)
	.macro blue_on
	.endmacro
	.macro blue_off
	.endmacro
.endif

;-- fet driving macros ---------------------------------------------------
; careful: "if" conditions split over multiple lines (with backslashes)
; work with arva, but avrasm2.exe silently produces wrong results.

.if !defined(high_side_pwm)
.equ high_side_pwm = 0
.endif

.if defined(anfet)
; traditional direct high and low side drive, inverted if init_px is set.
; dead-time insertion is supported for comp_pwm.

	.equ cpwm_soft = comp_pwm

	.macro fet_on
		.if (init_pb & ((@0 == portb) << @1)) | (init_pc & ((@0 == portc) << @1)) | (init_pd & ((@0 == portd) << @1))
		cbi	@0, @1
		.else
		sbi	@0, @1
		.endif
	.endmacro

	.macro fet_off
		.if (init_pb & ((@0 == portb) << @1)) | (init_pc & ((@0 == portc) << @1)) | (init_pd & ((@0 == portd) << @1))
		sbi	@0, @1
		.else
		cbi	@0, @1
		.endif
	.endmacro

	.macro anfet_on
		fet_on	anfet_port, anfet
	.endmacro
	.macro anfet_off
		fet_off	anfet_port, anfet
	.endmacro
	.macro apfet_on
		fet_on	apfet_port, apfet
	.endmacro
	.macro apfet_off
		fet_off	apfet_port, apfet
	.endmacro
	.macro bnfet_on
		fet_on	bnfet_port, bnfet
	.endmacro
	.macro bnfet_off
		fet_off	bnfet_port, bnfet
	.endmacro
	.macro bpfet_on
		fet_on	bpfet_port, bpfet
	.endmacro
	.macro bpfet_off
		fet_off	bpfet_port, bpfet
	.endmacro
	.macro cnfet_on
		fet_on	cnfet_port, cnfet
	.endmacro
	.macro cnfet_off
		fet_off	cnfet_port, cnfet
	.endmacro
	.macro cpfet_on
		fet_on	cpfet_port, cpfet
	.endmacro
	.macro cpfet_off
		fet_off	cpfet_port, cpfet
	.endmacro

	.macro pwm_focus_a_on
		.if comp_pwm
		cpse	temp3, temp4
		pwm_comp_a_on
		.endif
	.endmacro
	.macro pwm_focus_a_off
		.if comp_pwm
		in	temp3, pwm_comp_a_port_in
		pwm_comp_a_off
		in	temp4, pwm_comp_a_port_in
		.endif
	.endmacro
	.macro pwm_focus_b_on
		.if comp_pwm
		cpse	temp3, temp4
		pwm_comp_b_on
		.endif
	.endmacro
	.macro pwm_focus_b_off
		.if comp_pwm
		in	temp3, pwm_comp_b_port_in
		pwm_comp_b_off
		in	temp4, pwm_comp_b_port_in
		.endif
	.endmacro
	.macro pwm_focus_c_on
		.if comp_pwm
		cpse	temp3, temp4
		pwm_comp_c_on
		.endif
	.endmacro
	.macro pwm_focus_c_off
		.if comp_pwm
		in	temp3, pwm_comp_c_port_in
		pwm_comp_c_off
		in	temp4, pwm_comp_c_port_in
		.endif
	.endmacro

	; for pwm state mirroring in commutation routines
	.if high_side_pwm
		.equ	pwm_a_port_in = apfet_port
		.equ	pwm_b_port_in = bpfet_port
		.equ	pwm_c_port_in = cpfet_port
		.equ	pwm_comp_a_port_in = anfet_port
		.equ	pwm_comp_b_port_in = bnfet_port
		.equ	pwm_comp_c_port_in = cnfet_port
	.else
		.equ	pwm_a_port_in = anfet_port
		.equ	pwm_b_port_in = bnfet_port
		.equ	pwm_c_port_in = cnfet_port
		.equ	pwm_comp_a_port_in = apfet_port
		.equ	pwm_comp_b_port_in = bpfet_port
		.equ	pwm_comp_c_port_in = cpfet_port
	.endif

	.macro pwm_all_off
		.if high_side_pwm
		all_pfets_off @0
		.else
		all_nfets_off @0
		.endif
	.endmacro

	.macro all_pfets_off
	.if apfet_port != bpfet_port || apfet_port != cpfet_port
		apfet_off
		bpfet_off
		cpfet_off
	.else
		in	@0, apfet_port
		.if (init_pb & ((apfet_port == portb) << apfet)) | (init_pc & ((apfet_port == portc) << apfet)) | (init_pd & ((apfet_port == portd) << apfet))
		sbr	@0, (1<<apfet)+(1<<bpfet)+(1<<cpfet)
		.else
		cbr	@0, (1<<apfet)+(1<<bpfet)+(1<<cpfet)
		.endif
		out	apfet_port, @0
	.endif
	.endmacro

	.macro all_nfets_off
	.if anfet_port != bnfet_port || anfet_port != cnfet_port
		anfet_off
		bnfet_off
		cnfet_off
	.else
		in	@0, anfet_port
		.if (init_pb & ((anfet_port == portb) << anfet)) | (init_pc & ((anfet_port == portc) << anfet)) | (init_pd & ((anfet_port == portd) << anfet))
		sbr	@0, (1<<anfet)+(1<<bnfet)+(1<<cnfet)
		.else
		cbr	@0, (1<<anfet)+(1<<bnfet)+(1<<cnfet)
		.endif
		out	anfet_port, @0
	.endif
	.endmacro

	.macro nfet_brake
	.if anfet_port != bnfet_port || anfet_port != cnfet_port
		anfet_on
		bnfet_on
		cnfet_on
	.else
		in	@0, anfet_port
		.if (init_pb & ((anfet_port == portb) << anfet)) | (init_pc & ((anfet_port == portc) << anfet)) | (init_pd & ((anfet_port == portd) << anfet))
		cbr	@0, (1<<anfet)+(1<<bnfet)+(1<<cnfet)
		.else
		sbr	@0, (1<<anfet)+(1<<bnfet)+(1<<cnfet)
		.endif
		out	anfet_port, @0
	.endif
	.endmacro

.elif defined(enable_all)
; three logic level pwm/enable-style driver, with diode emulation mode or
; off state at the middle level on the pwm pin. this is accomplished by
; setting a pull-up rather than drive high. with this method, every fet
; toggle can be a single i/o instruction, rather than having to select
; high or low in advance to toggling enable. the following macros are used:
;
; xnfet_on  -> cbi pwm_x_port, pwm_x (drain through ext pull-down)
; xnfet_off -> sbi pwm_x_port, pwm_x (pull-up pin with ext pull-down)
; xpfet_on  -> sbi pwm_x_ddr, pwm_x (drive-up pin)
; xpfet_off -> cbi pwm_x_ddr, pwm_x (pull-up pin with ext pull-down)
;
; comp_pwm on these is done in hardware rather than software, so we can
; just toggle the port value after pwm_focus sets ddr (output mode).
; this results in the following macro arrangement:
;
; tri	cpwm	high_side_pwm	: pwm on	pwm off		pwm_x_port_in
; 0	0	0		: xnfet_on	xnfet_off	xnfet_port
; 0	0	1		: xpfet_on	xpfet_off	xpfet_port
; 0	1	0		: xnfet_on	xnfet_off	xnfet_port
; 0	1	1		: xpfet_on	xpfet_off	xpfet_port
; 1	0	0		: xnfet_on	xnfet_off	pwm_x_port
; 1	0	1		: xpfet_on	xpfet_off	pwm_x_ddr
; 1	1	0		: xnfet_on	xnfet_off	pwm_x_port
; 1	1	1		: xnfet_off	xnfet_on	pwm_x_port
;
; for the last case, pwm_x_off actually turns low side on which isn't how
; we want to leave the phase after commutating. pwm_x_clear will take care
; of this.
;
; we leave enable high once initialized as some drivers will actually
; shut down rather than just using the input as a logic gate.
;
; we prefer high_side_pwm as diode emulation mode in these drivers
; typically allows active freewheeling only in this orientation.

	.equ cpwm_soft = 0

	.macro anfet_on
		cbi	pwm_a_port, pwm_a
	.endmacro
	.macro anfet_off
		sbi	pwm_a_port, pwm_a
	.endmacro
	.macro apfet_on
		sbi	pwm_a_ddr, pwm_a
	.endmacro
	.macro apfet_off
		cbi	pwm_a_ddr, pwm_a
	.endmacro
	.macro bnfet_on
		cbi	pwm_b_port, pwm_b
	.endmacro
	.macro bnfet_off
		sbi	pwm_b_port, pwm_b
	.endmacro
	.macro bpfet_on
		sbi	pwm_b_ddr, pwm_b
	.endmacro
	.macro bpfet_off
		cbi	pwm_b_ddr, pwm_b
	.endmacro
	.macro cnfet_on
		cbi	pwm_c_port, pwm_c
	.endmacro
	.macro cnfet_off
		sbi	pwm_c_port, pwm_c
	.endmacro
	.macro cpfet_on
		sbi	pwm_c_ddr, pwm_c
	.endmacro
	.macro cpfet_off
		cbi	pwm_c_ddr, pwm_c
	.endmacro

	.macro pwm_focus_a_on
		.if comp_pwm
		sbrc	flags1, power_on
		apfet_on
		.endif
	.endmacro
	.macro pwm_focus_a_off
		.if comp_pwm
		apfet_off
		.endif
	.endmacro

	.macro pwm_focus_b_on
		.if comp_pwm
		sbrc	flags1, power_on
		bpfet_on
		.endif
	.endmacro
	.macro pwm_focus_b_off
		.if comp_pwm
		bpfet_off
		.endif
	.endmacro

	.macro pwm_focus_c_on
		.if comp_pwm
		sbrc	flags1, power_on
		cpfet_on
		.endif
	.endmacro
	.macro pwm_focus_c_off
		.if comp_pwm
		cpfet_off
		.endif
	.endmacro

	; for pwm state mirroring in commutation routines
	.if comp_pwm || !high_side_pwm
		.equ	pwm_a_port_in = pwm_a_port
		.equ	pwm_b_port_in = pwm_b_port
		.equ	pwm_c_port_in = pwm_c_port
	.else
		.equ	pwm_a_port_in = pwm_a_ddr
		.equ	pwm_b_port_in = pwm_b_ddr
		.equ	pwm_c_port_in = pwm_c_ddr
	.endif

	.macro pwm_all_off
		all_nfets_off @0
	.endmacro

	.macro all_pfets_off
		.if pwm_a_ddr != pwm_b_ddr || pwm_a_ddr != pwm_c_ddr
			apfet_off
			bpfet_off
			cpfet_off
		.else
			in	@0, pwm_a_ddr
			cbr	@0, (1<<pwm_a)+(1<<pwm_b)+(1<<pwm_c)
			out	pwm_a_ddr, @0
		.endif
	.endmacro

	.macro all_nfets_off
		.if pwm_a_port != pwm_b_port || pwm_a_port != pwm_c_port
			anfet_off
			bnfet_off
			cnfet_off
		.else
			in	@0, pwm_a_port
			sbr	@0, (1<<pwm_a)+(1<<pwm_b)+(1<<pwm_c)
			out	pwm_a_port, @0
		.endif
	.endmacro

	.macro nfet_brake
		.if pwm_a_port != pwm_b_port || pwm_a_port != pwm_c_port
			anfet_on
			bnfet_on
			cnfet_on
		.else
			in	@0, pwm_a_port
			cbr	@0, (1<<pwm_a)+(1<<pwm_b)+(1<<pwm_c)
			out	pwm_a_port, @0
		.endif
	.endmacro

.endif

;-- commutation drive macros ---------------------------------------------

.if high_side_pwm
	.macro commutate_a_on
		anfet_on
	.endmacro
	.macro commutate_a_off
		anfet_off
	.endmacro
	.macro commutate_b_on
		bnfet_on
	.endmacro
	.macro commutate_b_off
		bnfet_off
	.endmacro
	.macro commutate_c_on
		cnfet_on
	.endmacro
	.macro commutate_c_off
		cnfet_off
	.endmacro
.else
	.macro commutate_a_on
		apfet_on
	.endmacro
	.macro commutate_a_off
		apfet_off
	.endmacro
	.macro commutate_b_on
		bpfet_on
	.endmacro
	.macro commutate_b_off
		bpfet_off
	.endmacro
	.macro commutate_c_on
		cpfet_on
	.endmacro
	.macro commutate_c_off
		cpfet_off
	.endmacro
.endif

;-- pwm macros -----------------------------------------------------------

.macro pwm_a_on
	.if !defined(anfet) && comp_pwm && high_side_pwm
		anfet_off
	.elif high_side_pwm
		apfet_on
	.else
		anfet_on
	.endif
.endmacro
.macro pwm_a_off
	.if !defined(anfet) && comp_pwm && high_side_pwm
		anfet_on
	.elif high_side_pwm
		apfet_off
	.else
		anfet_off
	.endif
.endmacro
.macro pwm_a_clear
		in	temp1, pwm_a_port_in
	.if !defined(anfet) && comp_pwm && high_side_pwm
		anfet_off
	.elif high_side_pwm
		apfet_off
	.else
		anfet_off
	.endif
		in	temp2, pwm_a_port_in
.endmacro
.macro pwm_a_copy
		cpse	temp1, temp2
	.if !defined(anfet) && comp_pwm && high_side_pwm
		anfet_on
	.elif high_side_pwm
		apfet_on
	.else
		anfet_on
	.endif
.endmacro

.macro pwm_b_on
	.if !defined(bnfet) && comp_pwm && high_side_pwm
		bnfet_off
	.elif high_side_pwm
		bpfet_on
	.else
		bnfet_on
	.endif
.endmacro
.macro pwm_b_off
	.if !defined(bnfet) && comp_pwm && high_side_pwm
		bnfet_on
	.elif high_side_pwm
		bpfet_off
	.else
		bnfet_off
	.endif
.endmacro
.macro pwm_b_clear
		in	temp1, pwm_b_port_in
	.if !defined(bnfet) && comp_pwm && high_side_pwm
		bnfet_off
	.elif high_side_pwm
		bpfet_off
	.else
		bnfet_off
	.endif
		in	temp2, pwm_b_port_in
.endmacro
.macro pwm_b_copy
		cpse	temp1, temp2
	.if !defined(bnfet) && comp_pwm && high_side_pwm
		bnfet_on
	.elif high_side_pwm
		bpfet_on
	.else
		bnfet_on
	.endif
.endmacro

.macro pwm_c_on
	.if !defined(cnfet) && comp_pwm && high_side_pwm
		cnfet_off
	.elif high_side_pwm
		cpfet_on
	.else
		cnfet_on
	.endif
.endmacro
.macro pwm_c_off
	.if !defined(cnfet) && comp_pwm && high_side_pwm
		cnfet_on
	.elif high_side_pwm
		cpfet_off
	.else
		cnfet_off
	.endif
.endmacro
.macro pwm_c_clear
		in	temp1, pwm_c_port_in
	.if !defined(cnfet) && comp_pwm && high_side_pwm
		cnfet_off
	.elif high_side_pwm
		cpfet_off
	.else
		cnfet_off
	.endif
		in	temp2, pwm_c_port_in
.endmacro
.macro pwm_c_copy
		cpse	temp1, temp2
	.if !defined(cnfet) && comp_pwm && high_side_pwm
		cnfet_on
	.elif high_side_pwm
		cpfet_on
	.else
		cnfet_on
	.endif
.endmacro

;-- complementary pwm macros ---------------------------------------------

.if cpwm_soft
	.macro pwm_comp_a_on
		.if high_side_pwm
			anfet_on
		.else
			apfet_on
		.endif
	.endmacro

	.macro pwm_comp_a_off
		.if high_side_pwm
			anfet_off
		.else
			apfet_off
		.endif
	.endmacro

	.macro pwm_comp_b_on
		.if high_side_pwm
			bnfet_on
		.else
			bpfet_on
		.endif
	.endmacro

	.macro pwm_comp_b_off
		.if high_side_pwm
			bnfet_off
		.else
			bpfet_off
		.endif
	.endmacro

	.macro pwm_comp_c_on
		.if high_side_pwm
			cnfet_on
		.else
			cpfet_on
		.endif
	.endmacro

	.macro pwm_comp_c_off
		.if high_side_pwm
			cnfet_off
		.else
			cpfet_off
		.endif
	.endmacro
.endif

;-- rc pulse setup and edge handling macros ------------------------------

.if use_icp
.macro rcp_int_enable
		in	@0, timsk
		sbr	@0, (1<<ticie1)	; enable icp1_int
		out	timsk, @0
.endmacro
.macro rcp_int_disable
		in	@0, timsk
		cbr	@0, (1<<ticie1)	; disable icp1_int
		out	timsk, @0
.endmacro
.macro rcp_int_rising_edge
		ldi	@0, t1clk
		out	tccr1b, @0
.endmacro
.macro rcp_int_falling_edge
		ldi	@0, t1clk & ~(1<<ices1)
		out	tccr1b, @0
.endmacro
.elif use_int0
.macro rcp_int_enable
		ldi	@0, (1<<int0)	; enable ext_int0
		out	gicr, @0
.endmacro
.macro rcp_int_disable
		out	gicr, zh	; disable ext_int0
.endmacro
.if use_int0 == 1
.macro rcp_int_rising_edge
		ldi	@0, (1<<isc01)+(1<<isc00)
		out	mcucr, @0	; set next int0 to rising edge
.endmacro
.macro rcp_int_falling_edge
		ldi	@0, (1<<isc01)
		out	mcucr, @0	; set next int0 to falling edge
.endmacro
.elif use_int0 == 2
.macro rcp_int_rising_edge
		ldi	@0, (1<<isc01)
		out	mcucr, @0	; set next int0 to falling edge
.endmacro
.macro rcp_int_falling_edge
		ldi	@0, (1<<isc01)+(1<<isc00)
		out	mcucr, @0	; set next int0 to rising edge
.endmacro
.endif
.endif

;-- analog comparator sense macros ---------------------------------------
; we enable and disable the adc to override acme when one of the sense
; pins is ain1 instead of an adc pin. in the future, this will allow
; reading from the adc at the same time.

.macro comp_init
		in	@0, sfior
		sbr	@0, (1<<acme)	; set analog comparator multiplexer enable
		out	sfior, @0
	.if defined(mux_a) && defined(mux_b) && defined(mux_c)
		cbi	adcsra, aden	; disable adc to make sure acme works
	.endif
.endmacro
.macro comp_adc_disable
	.if !defined(mux_a) || !defined(mux_b) || !defined(mux_c)
		cbi	adcsra, aden	; disable adc if we enabled it to get ain1
	.endif
.endmacro
.macro comp_adc_enable
		sbi	adcsra, aden	; eisable adc to effectively disable acme
.endmacro
.macro set_comp_phase_a
	.if defined(mux_a)
		ldi	@0, mux_a	; set comparator multiplexer to phase a
		out	admux, @0
		comp_adc_disable
	.else
		comp_adc_enable
	.endif
.endmacro
.macro set_comp_phase_b
	.if defined(mux_b)
		ldi	@0, mux_b	; set comparator multiplexer to phase b
		out	admux, @0
		comp_adc_disable
	.else
		comp_adc_enable
	.endif
.endmacro
.macro set_comp_phase_c
	.if defined(mux_c)
		ldi	@0, mux_c	; set comparator multiplexer to phase c
		out	admux, @0
		comp_adc_disable
	.else
		comp_adc_enable
	.endif
.endmacro

;-- timing and motor debugging macros ------------------------------------

.macro flag_on
	.if motor_debug && (dir_pb & (1<<4)) == 0
		sbi	portb, 4
	.endif
.endmacro
.macro flag_off
	.if motor_debug && (dir_pb & (1<<4)) == 0
		cbi	portb, 4
	.endif
.endmacro
.macro sync_on
	.if motor_debug && (dir_pb & (1<<3)) == 0
		sbi	portb, 3
	.elif motor_debug && (dir_pb & (1<<5)) == 0
		sbi	portb, 5
	.endif
.endmacro
.macro sync_off
	.if motor_debug && (dir_pb & (1<<3)) == 0
		cbi	portb, 3
	.elif motor_debug && (dir_pb & (1<<5)) == 0
		cbi	portb, 5
	.endif
.endmacro

; short cycle delay without clobbering flags
.equ	max_busy_wait_cycles	= 32
.macro cycle_delay
.if @0 >= max_busy_wait_cycles
.error "cycle_delay too long"
.endif
.if @0 > 0
	.if @0 & 1
		nop
	.endif
	.if @0 & 2
		rjmp	pc + 1
	.endif
	.if @0 & 4
		rjmp	pc + 1
		rjmp	pc + 1
	.endif
	.if @0 & 8
		nop
		rcall	wait_ret		; 3 cycles to call + 4 to return
	.endif
	.if @0 & 16
		rjmp	pc + 1
		rcall	wait_ret
		rcall	wait_ret
	.endif
.endif
.endmacro

;-----bko-----------------------------------------------------------------
; timer2 overflow interrupt (output pwm) -- the interrupt vector actually
; "ijmp"s to z, which should point to one of these entry points.
;
; we try to avoid clobbering (and thus needing to save/restore) flags;
; in, out, mov, ldi, cpse, etc. do not modify any flags, while dec does.
;
; we used to check the comparator (acsr) here to help starting, since pwm
; switching is what introduces noise that affects the comparator result.
; however, timing of this is very sensitive to fet characteristics, and
; would work well on some boards but not at all on others without waiting
; another 100-200ns, which was enough to break other boards. so, instead,
; we do all of the acsr sampling outside of the interrupt and do digital
; filtering. the avr interrupt overhead also helps to shield the noise.
;
; we reload tcnt2 as the very last step so as to reduce pwm dead areas
; between the reti and the next interrupt vector execution, which still
; takes a good 4 (reti) + 4 (interrupt call) + 2 (ijmp) cycles. we also
; try to keep the switch on close to the start of pwm_on and switch off
; close to the end of pwm_aff to minimize the power bump at full power.
;
; pwm_*_high and pwm_again are called when the particular on/off cycle
; is longer than will fit in 8 bits. this is tracked in tcnt2h.

.if motor_brake || low_brake
pwm_brake_on:
		cpse	tcnt2h, zh
		rjmp	pwm_again
		in	i_sreg, sreg
		nfet_brake i_temp1
		ldi	i_temp1, 0xff
		cp	off_duty_l, i_temp1	; check for 0 off-time
		cpc	off_duty_h, zh
		breq	pwm_brake_on1
		ldi	zl, pwm_brake_off	; not full on, so turn it off next
		lds	i_temp2, brake_sub
		sub	sys_control_l, i_temp2
		brne	pwm_brake_on1
		neg	duty_l			; increase duty
		sbc	duty_h, i_temp1		; i_temp1 is 0xff aka -1
		com	duty_l
		com	off_duty_l		; decrease off duty
		sbc	off_duty_l, zh
		sbc	off_duty_h, zh
		com	off_duty_l
pwm_brake_on1:	mov	tcnt2h, duty_h
		out	sreg, i_sreg
		out	tcnt2, duty_l
		reti

pwm_brake_off:
		cpse	tcnt2h, zh
		rjmp	pwm_again
		in	i_sreg, sreg
		ldi	zl, pwm_brake_on
		mov	tcnt2h, off_duty_h
		all_nfets_off i_temp1
		out	sreg, i_sreg
		out	tcnt2, off_duty_l
		reti
.endif

.if dead_time_high > 7
.equ	extra_dead_time_high = dead_time_high - 7
.else
.equ	extra_dead_time_high = 0
.endif

pwm_on_fast_high:
.if cpwm_soft && extra_dead_time_high > max_busy_wait_cycles
		in	i_sreg, sreg
		dec	tcnt2h
		brne	pwm_on_fast_high_again
		ldi	zl, pwm_on_fast
pwm_on_fast_high_again:
		out	sreg, i_sreg
		reti
.endif

pwm_on_high:
		in	i_sreg, sreg
		dec	tcnt2h
		brne	pwm_on_again
		ldi	zl, pwm_on
pwm_on_again:	out	sreg, i_sreg
		reti

pwm_again:
		in	i_sreg, sreg
		dec	tcnt2h
		out	sreg, i_sreg
		reti

pwm_on:
.if cpwm_soft
		sbrc	flags2, a_fet
		pwm_comp_a_off
		sbrc	flags2, b_fet
		pwm_comp_b_off
		sbrc	flags2, c_fet
		pwm_comp_c_off
	.if extra_dead_time_high > max_busy_wait_cycles
		; reschedule to interrupt once the dead time has passed
		.if high(extra_dead_time_high)
		ldi	i_temp1, high(extra_dead_time_high)
		mov	tcnt2h, i_temp1
		ldi	zl, pwm_on_fast_high
		.else
		ldi	zl, pwm_on_fast
		.endif
		ldi	i_temp1, 0xff - low(extra_dead_time_high)
		out	tcnt2, i_temp1
		reti				; do something else while we wait
		.equ	cpwm_overhead_high = 7 + 8 + extra_dead_time_high
	.else
		; waste cycles to wait for the dead time
		cycle_delay extra_dead_time_high
		.equ	cpwm_overhead_high = 7 + extra_dead_time_high
		; fall through
	.endif
.endif
pwm_on_fast:
		sbrc	flags2, a_fet
		pwm_a_on
		sbrc	flags2, b_fet
		pwm_b_on
		sbrc	flags2, c_fet
		pwm_c_on
		ldi	zl, pwm_off
		mov	tcnt2h, duty_h
		out	tcnt2, duty_l
		reti

pwm_wdr:					; just reset watchdog
		wdr
		reti

pwm_off:
		cpse	tcnt2h, zh		; 2 cycles to skip when tcnt2h is 0
		rjmp	pwm_again
		wdr				; 1 cycle: watchdog reset
		sbrc	flags1, full_power	; 2 cycles to skip if not full power
		rjmp	pwm_on			; none of this off stuff if full power
		lds	zl, pwm_on_ptr		; 2 cycles
		mov	tcnt2h, off_duty_h	; 1 cycle
		sbrc	flags2, a_fet		; 2 cycles if skip, 1 cycle otherwise
		pwm_a_off			; 2 cycles (off at 12 cycles from entry)
		sbrc	flags2, b_fet		; offset by 2 cycles here,
		pwm_b_off			; but still equal on-time
		sbrc	flags2, c_fet
		pwm_c_off
		out	tcnt2, off_duty_l	; 1 cycle
		.if cpwm_soft
		sbrc	flags2, skip_cpwm	; 2 cycles if skip, 1 cycle otherwise
		reti
		.if dead_time_low > 9
		.equ	extra_dead_time_low = dead_time_low - 9
		.else
		.equ	extra_dead_time_low = 0
		.endif
		cycle_delay extra_dead_time_low - 2
		.equ	cpwm_overhead_low = 9 + extra_dead_time_low
		sbrc	flags2, a_fet
		pwm_comp_a_on
		sbrc	flags2, b_fet
		pwm_comp_b_on
		sbrc	flags2, c_fet
		pwm_comp_c_on
		.endif
		reti				; 4 cycles

.if high(pwm_off)
.error "high(pwm_off) is non-zero; please move code closer to start or use 16-bit (zh) jump registers"
.endif
;-----bko-----------------------------------------------------------------
; timer1 output compare interrupt
t1oca_int:	in	i_sreg, sreg
		lds	i_temp1, ocr1ax
		subi	i_temp1, 1
		brcc	t1oca_int1
		cbr	flags0, (1<<oct1_pending)	; signal oct1a passed
t1oca_int1:	sts	ocr1ax, i_temp1
		out	sreg, i_sreg
		reti
;-----bko-----------------------------------------------------------------
; timer1 overflow interrupt (happens every 4096µs)
t1ovfl_int:	in	i_sreg, sreg
		lds	i_temp1, tcnt1x
		inc	i_temp1
		sts	tcnt1x, i_temp1
		.if beacon_idle
		brne	t1ovfl_int0
		lds	i_temp2, idle_beacon
		inc	i_temp2
		sts	idle_beacon, i_temp2
t1ovfl_int0:
		.endif
		andi	i_temp1, 15			; every 16 overflows
		brne	t1ovfl_int1
		tst	rc_timeout
		breq	t1ovfl_int2
		dec	rc_timeout
t1ovfl_int1:	out	sreg, i_sreg
		reti
t1ovfl_int2:	lds	i_temp1, rct_boot
		inc	i_temp1
		sts	rct_boot, i_temp1
		lds	i_temp1, rct_beacon
		inc	i_temp1
		sts	rct_beacon, i_temp1
		rjmp	t1ovfl_int1
;-----bko-----------------------------------------------------------------
; note: this interrupt uses the 16-bit atomic timer read/write register
; by reading tcnt1l and tcnt1h, so this interrupt must be disabled before
; any other 16-bit timer options happen that might use the same register
; (see "accessing 16-bit registers" in the atmel documentation)
; icp1 = rc pulse input, if enabled
rcp_int:
	.if use_icp || use_int0
		.if use_icp
		in	i_temp1, icr1l		; get captured timer values
		in	i_temp2, icr1h
		in	i_sreg, tccr1b		; abuse i_sreg to hold value
		sbrs	i_sreg, ices1		; evaluate edge of this interrupt
		.else
		in	i_temp1, tcnt1l		; get timer1 values
		in	i_temp2, tcnt1h
		.if use_int0 == 1
		sbis	pind, rcp_in		; evaluate edge of this interrupt
		.else
		sbic	pind, rcp_in		; inverted signalling
		.endif
		.endif
		rjmp	falling_edge
rising_edge:
		in	i_sreg, sreg
		; stuff this rise time plus max_rc_puls into ocr1b.
		; we use this both to save the time it went high and
		; to get an interrupt to indicate high timeout.
		adiwx	i_temp1, i_temp2, max_rc_puls * cpu_mhz
		out	ocr1bh, i_temp2
		out	ocr1bl, i_temp1
		rcp_int_falling_edge i_temp1	; set next int to falling edge
		ldi	i_temp1, (1<<ocf1b)	; clear ocf1b flag
		out	tifr, i_temp1
		out	sreg, i_sreg
		reti

rcpint_fail:
		in	i_sreg, sreg
		sbr	flags0, (1<<rcp_error)
		rjmp	rcpint_exit

falling_edge:
		in	i_sreg, tifr
		sbrc	i_sreg, ocf1b		; too long high would set ocf1b
		rjmp	rcpint_fail
		in	i_sreg, sreg
		movw	rx_l, i_temp1		; guaranteed to be valid, store immediately
		in	i_temp1, ocr1bl		; no atomic temp register used to read ocr1* registers
		in	i_temp2, ocr1bh
		sbi2	i_temp1, i_temp2, max_rc_puls * cpu_mhz	; put back to start time
		sub	rx_l, i_temp1		; subtract start time from current time
		sbc	rx_h, i_temp2
.if max_rc_puls * cpu_mhz > 0xffff
.error "max_rc_puls * cpu_mhz too big to fit in two bytes -- adjust it or the rcp_int code"
.endif
		sbr	flags1, (1<<eval_rc)
rcpint_exit:	rcp_int_rising_edge i_temp1	; set next int to rising edge
		out	sreg, i_sreg
		reti
	.endif
;-----bko-----------------------------------------------------------------
; mk bl-ctrl v1, v2 compatible input control
; ctrl-click settings in mktool for reversing and additional settings
i2c_int:
	.if use_i2c
		in	i_sreg, sreg
		in	i_temp1, twsr
		cpi	i_temp1, 0x60		; rx: received our sla+w
		breq	i2c_ack
		cpi	i_temp1, 0x80		; rx: data available, previously acked
		breq	i2c_rx_data
		cpi	i_temp1, 0xa0		; rx: stop/restart condition (end of message)
		breq	i2c_rx_stop
		cpi	i_temp1, 0xa8		; tx: received our sla+r
		breq	i2c_tx_init
		cpi	i_temp1, 0xb8		; tx: data request, previously acked
		breq	i2c_tx_data
		cpi	i_temp1, 0xf8		; tx: no relevant state information
		breq	i2c_io_error
		cpse	i_temp1, zh		; bus error due to illegal start/stop condition
		rjmp	i2c_ack			; 0x88, 0xc0, etc.: enable listening
i2c_io_error:	ldi	i_temp1, (1<<twie)|(1<<twen)|(1<<twsto)|(1<<twea)|(1<<twint)
		rjmp	i2c_out

i2c_tx_init:	sbrc	rx_l, 7			; blconfig struct requested?
		rjmp	i2c_tx_blconfig
		out	twdr, zh		; send 0 as current (dummy)
		ldi	i_temp1, 250		; prepare maxpwm value (250 when stopped enables mk bl-ctrl proto v2)
		sbrc	flags1, power_on
i2c_tx_datarep:	ldi	i_temp1, 255		; send maxpwm 255 when running (and repeat for temperature)
		sts	i2c_max_pwm, i_temp1
		rjmp	i2c_ack
i2c_tx_data:	sbrc	rx_l, 7			; blconfig struct requested?
		rjmp	i2c_tx_blconfig1
		lds	i_temp1, i2c_max_pwm	; maxpwm value (has special meaning for mk)
		out	twdr, i_temp1
		rjmp	i2c_tx_datarep		; send 255 for temperature for which we should get a nack (0xc0)

i2c_tx_blconfig:
		ldi	i_temp1, blc_revision	; first blconfig structure member
		sts	i2c_blc_offset, i_temp1
i2c_tx_blconfig1:
		mov	i_temp2, zl		; save z
		lds	zl, i2c_blc_offset
		ld	i_temp1, z+
		sts	i2c_blc_offset, zl
		out	twdr, i_temp1
		cpi	zl, blc_checksum + 1	; past last structure member?
		mov	zl, i_temp2		; restore z
		breq	i2c_nack		; no more space
		rjmp	i2c_ack

i2c_nack:	ldi	i_temp1, (1<<twie)|(1<<twen)|(1<<twint)
		rjmp	i2c_out
i2c_ack:	ldi	i_temp1, (1<<twie)|(1<<twen)|(1<<twea)|(1<<twint)
i2c_out:	out	twcr, i_temp1
		out	sreg, i_sreg
		reti

i2c_rx_stop:	lds	i_temp1, i2c_rx_state
		cpse	i_temp1, zh		; skip if empty message or we were writing
		sbr	flags1, (1<<eval_rc)|(1<<i2c_mode)	; i2c message received
		sts	i2c_rx_state, zh
		rjmp	i2c_ack
i2c_rx_data:	lds	i_temp1, i2c_rx_state
		inc	i_temp1
		sts	i2c_rx_state, i_temp1
		cpi	i_temp1, 1
		brne	i2c_rx_data1
		in	rx_h, twdr		; receive high byte from bus
		mov	rx_l, zh		; zero low byte (we may not receive it)
		rjmp	i2c_ack
i2c_rx_data1:	cpi	i_temp1, 2
		brne	i2c_rx_blc
		in	rx_l, twdr		; receive low byte from bus
		rjmp	i2c_ack

i2c_rx_blc:	cpi	i_temp1, 3		; blconfig revision
		brne	i2c_rx_blc1
		in	i_temp1, twdr
		cpi	i_temp1, 2
		brne	i2c_nack		; we support only blconfig revision 2
		rjmp	i2c_ack
i2c_rx_blc1:	cpi	i_temp1, 3 + blc_checksum - blc_revision	; checksum field?
		breq	i2c_rx_blccsum
		mov	i_temp2, zl		; save z
		ldi	zl, blc_revision - 3
		add	zl, i_temp1		; z now points to blconfig structure member
		in	i_temp1, twdr		; read blconfig data byte
		st	z, i_temp1		; update structure member
		mov	zl, i_temp2		; restore z
		lds	i_temp2, blc_checksum
		add	i_temp2, i_temp1
		sts	blc_checksum, i_temp2
		rjmp	i2c_ack			; more expected
i2c_rx_blccsum:	in	i_temp1, twdr		; we can't do anything with the checksum, so just update it to remove setmask
		lds	i_temp2, blc_setmask	; after receiving, zero the settings mask
;out udr, i_temp2
		sbrc	i_temp2, 6		; reset eeprom if bit 6 set in blc_setmask
		sbr	flags0, (1<<eeprom_reset)
		sbrc	i_temp2, 7		; write eeprom if bit 7 set in blc_setmask
		sbr	flags0, (1<<eeprom_write)
		sub	i_temp1, i_temp2
		ldi	i_temp2, 0b10010000	; default to write eeprom and reverse direction options selected
		add	i_temp1, i_temp2
		sts	blc_setmask, i_temp2
		sts	blc_checksum, i_temp1
		rjmp	i2c_ack
	.endif
;-----bko-----------------------------------------------------------------
urxc_int:
; this is bernhard's serial protocol implementation in the uart
; version here: http://home.versanet.de/~b-konze/blc_6a/blc_6a.htm
; this seems to be implemented for a project described here:
; http://www.control.aau.dk/uav/reports/10gr833/10gr833_student_report.pdf
; the uart runs at 38400 baud, n81. input is ignored until >= 0xf5
; is received, where we start counting to motor_id, at which
; the received byte is used as throttle input. 0 is neutral,
; >= 200 is full_power.
	.if use_uart
		in	i_sreg, sreg
		in	i_temp1, udr
		cpi	i_temp1, 0xf5		; start throttle byte sequence
		breq	urxc_x3d_sync
		sbrs	flags0, uart_sync
		rjmp	urxc_exit		; throw away if not uart_sync
		brcc	urxc_unknown
		lds	i_temp2, motor_count
		dec	i_temp2
		brne	urxc_set_exit		; skip when motor_count != 0
		mov	rx_h, i_temp1		; save 8-bit input
		sbr	flags1, (1<<eval_rc)+(1<<uart_mode)
urxc_unknown:	cbr	flags0, (1<<uart_sync)
		rjmp	urxc_exit
urxc_x3d_sync:	sbr	flags0, (1<<uart_sync)
		ldi	i_temp2, motor_id	; start counting down from motor_id
urxc_set_exit:	sts	motor_count, i_temp2
urxc_exit:	out	sreg, i_sreg
		reti
	.endif
;-----bko-----------------------------------------------------------------
; beeper: timer0 is set to 1µs/count
beep_f1:	ldi	temp2, 80
		ldi	temp4, 200
		red_on
		blue_on
beep_f1_on:	bpfet_on
		anfet_on
		rcall	beep
		brne	beep_f1_on
		blue_off
		red_off
		ret

beep_f2:	ldi	temp2, 100
		ldi	temp4, 180
		grn_on
		blue_on
beep_f2_on:	cpfet_on
		bnfet_on
		rcall	beep
		brne	beep_f2_on
		blue_off
		grn_off
		ret

beep_f3:	ldi	temp2, 120
		ldi	temp4, 160
		blue_on
beep_f3_on:	apfet_on
		cnfet_on
		rcall	beep
		brne	beep_f3_on
		blue_off
		ret

beep_f4:	ldi	temp2, 140
beep_f4_freq:	ldi	temp4, 140
beep_f4_fets:	red_on
		grn_on
		blue_on
beep_f4_on:	cpfet_on
		anfet_on
		rcall	beep
		brne	beep_f4_on
		blue_off
		grn_off
		red_off
		ret

		; fall through
;-----bko-----------------------------------------------------------------
; interrupts no longer need to be disabled to beep, but the pwm interrupt
; must be muted first
beep:		out	tcnt0, zh
beep1:		in	temp1, tcnt0
		cpi	temp1, 2*cpu_mhz	; 32µs on
		brlo	beep1
		all_pfets_off temp3
		all_nfets_off temp3
		ldi	temp3, cpu_mhz
beep2:		out	tcnt0, zh
		wdr
beep3:		in	temp1, tcnt0
		cp	temp1, temp4
		brlo	beep3
		dec	temp3
		brne	beep2
		dec	temp2
		ret

wait240ms:	rcall	wait120ms
wait120ms:	rcall	wait60ms
wait60ms:	rcall	wait30ms
wait30ms:	ldi	temp2, 15
wait1:		ldi	temp3, cpu_mhz
wait2:		out	tcnt0, zh
		ldi	temp1, (1<<tov0)	; clear tov0 by setting it
		out	tifr, temp1
		wdr
wait3:		in	temp1, tifr
		sbrs	temp1, tov0
		rjmp	wait3
		dec	temp3
		brne	wait2
		dec	temp2
		brne	wait1
wait_ret:	ret

;-- eeprom functions -----------------------------------------------------
; interrupts must be disabled to avoid z conflicts and content changes.
eeprom_check_reset:
	; check eeprom signature
		lds	temp1, eeprom_sig_l
		lds	temp2, eeprom_sig_h
		subi	temp1, low(eeprom_sign)
		sbci	temp2, high(eeprom_sign)
		breq	eeprom_good

	; signature not good: set defaults in ram, but do not write
	; to the eeprom until we actually set something non-default
eeprom_reset1:	ldi2	yl, yh, eeprom_sig_l
		ldi	zl, low(eeprom_defaults_w << 1)
eeprom_reset2:	lpm	temp1, z+
		st	y+, temp1
		cpi	yl, low(eeprom_end)
		brne	eeprom_reset2
eeprom_good:	ret

.if use_i2c
eeprom_reset_block:
		cli
		push	zl
		rcall	eeprom_reset1
		pop	zl
		sei
		ret
.endif

;-----bko-----------------------------------------------------------------
; read from or write to the eeprom block. to avoid duplication, we use the
; global interrupts flag (i) to enable writing versus reading mde. only
; changed bytes are written. we restore osccal to the boot-time value as
; the eeprom timing is affected by it. we always return by falling through
; to osccal_set.
eeprom_read_block:				; when interrupts disabled
eeprom_write_block:				; when interrupts enabled
		lds	temp1, orig_osccal
		out	osccal, temp1
		cbr	flags0, (1<<eeprom_write)
		ldi2	yl, yh, eeprom_sig_l
		ldi2	temp1, temp2, eeprom_offset
eeprom_rw1:	wdr
		sbic	eecr, eewe
		rjmp	eeprom_rw1		; loop while writing eeprom
		in	temp3, spmcr
		sbrc	temp3, spmen
		rjmp	eeprom_rw1		; loop while flashing
		cpi	yl, low(eeprom_end)
		breq	eeprom_rw4
		out	eearh, temp2
		out	eearl, temp1
		adiw	temp1, 1
		sbi	eecr, eere		; read existing eeprom byte
		in	temp3, eedr
		brie	eeprom_rw2
		st	y+, temp3		; store the byte to ram
		rjmp	eeprom_rw1
eeprom_rw2:	ld	temp4, y+		; compare with the byte in ram
		out	eedr, temp4
		cli
		sbi	eecr, eemwe
		cpse	temp3, temp4
		sbi	eecr, eewe
		sei
		rjmp	eeprom_rw1
eeprom_rw4:	rcall	wait30ms
		; fall through to set the oscillator calibration
;-----bko-----------------------------------------------------------------
; set the oscillator calibration for 8mhz operation, or set it to 0xff for
; approximately 16mhz operation even without an external oscillator. this
; should be safe as long as we restore it during eeprom accesses. this
; will have no effect on boards with external oscillators, except that
; the eeprom still uses the internal oscillator (at 1mhz).
osccal_set:
.if cpu_mhz == 16
		ldi	temp1, 0xff		; almost 16mhz
.else
		ldi	temp1, 0x9f		; almost 8mhz
.endif
		out	osccal, temp1
		ret
;-----bko-----------------------------------------------------------------
; shift left temp7:temp6:temp5 temp1 times.
lsl_temp567:
		lsl	temp5
		rol	temp6
		rol	temp7
		dec	temp1
		brne	lsl_temp567
		ret
;-----bko-----------------------------------------------------------------
; multiply temp1:temp2 by temp3:temp4 and add high 16 bits of result to y.
; clobbers temp5, temp6, and leaves the lower byte in temp7.
mul_y_12x34:
		mul	temp1, temp3		; scale raw pulse length to power_range: 16x16->32 (bottom 16 discarded)
		mov	temp7, temp6		; save byte 2 of result, discard byte 1 already
		mul	temp2, temp3
		add	temp7, temp5
		adc	yl, temp6
		adc	yh, zh
		mul	temp1, temp4
		add	temp7, temp5
		adc	yl, temp6
		adc	yh, zh
		mul	temp2, temp4
		add	yl, temp5
		adc	yh, temp6		; product is now in y, flags set
		ret

;-- hardware diagnostics -------------------------------------------------
; any brushless esc based on the atmega8 or similar must tie the sense
; neutral star to ain0, and the three sense lines to three adc pins or
; two adc pins and ain1. all of these pins are also normal i/o pins, so
; we can drive them and see if the adc values move. a value that does not
; move indicates a shorted fet or that an incorrect board target has been
; flashed.
;
; note: some fet drivers such as the lm5109 can pull up the output a bit,
; making the "stuck high" test return a false positive. perhaps it would
; be sufficient to test that the phases read as 0 _or_ can be pulled down
; by driving ain0 to ground.
;
; in typical conditions on the atmega8, i/o pins transition to low at
; about 1.42v and to high at about 1.86v. the adc is 10-bit, however, and
; should work even with a strong sense voltage divider.
;
; throughout all of this, the motor may be spinning. if so, we should wait
; long enough that each phase falls to 0v and all tests succeed.
;
.if check_hardware
.set adc_read_needed = 1
.equ max_check_loops = 5000			; adc check takes ~200us

hardware_check:
		clt

		; first, check that all sense lines are low.
		.if defined(mux_a)
		ldi	xl, 1			; error code 1: phase a stuck high
		ldi	temp4, mux_a
		rcall	check_sense_low
		.endif

		.if defined(mux_b)
		ldi	xl, 2			; error code 2: phase b stuck high
		ldi	temp4, mux_b
		rcall	check_sense_low
		.endif

		.if defined(mux_c)
		ldi	xl, 3			; error code 3: phase c stuck high
		ldi	temp4, mux_c
		rcall	check_sense_low
		.endif

		.if !defined(mux_a) || !defined(mux_b) || !defined(mux_c)
		ldi	xl, 4			; error code 4: ain1 stuck high
		ldi2	yl, yh, max_check_loops
check_ain1_low:	sbiw	yl, 1
		sbic	pind, 7			; skip loop if ain1 low
		brne	check_ain1_low
		rcall	hw_error_eq
		.endif

		ldi	xl, 5			; error code 5: ain0 stuck high
		ldi2	yl, yh, max_check_loops
check_ain0_low:	sbiw	yl, 1
		sbic	pind, 6			; skip loop if ain0 low
		brne	check_ain0_low
		rcall	hw_error_eq

		brts	hardware_check		; do not allow further tests if stuck high

		; if nothing is stuck high, pull up the motor by driving
		; the emulated neutral (ain0) high, and try driving each
		; phase low. while driven, leakage through the star is
		; eliminated, so one phase will not influence another
		; unless a motor is connected. a phase on ain1 cannot be
		; read by the adc, so we must skip it.

		sbi	ddrd, 6
		sbi	portd, 6		; drive ain0 high

		.if defined(mux_a)
		rcall	wait30ms		; there might be some capacitance
		ldi	xl, 6			; error code 6: phase a low-side drive broken
		ldi	temp4, mux_a
		rcall	adc_read
		movw	yl, temp1		; save adc value (hopefully non-zero)
		anfet_on			; drive down this phase (we've established that it was 0v above).
		rcall	adc_read		; fet turn-on will easily beat adc initialization
		anfet_off
		rcall	hw_error_y_le_temp12
		.endif

		.if defined(mux_b)
		rcall	wait30ms
		ldi	xl, 7			; error code 7: phase b low-side drive broken
		ldi	temp4, mux_b
		rcall	adc_read
		movw	yl, temp1		; save adc value (hopefully non-zero)
		bnfet_on			; drive down this phase (we've established that it was 0v above).
		rcall	adc_read		; fet turn-on will easily beat adc initialization
		bnfet_off
		rcall	hw_error_y_le_temp12
		.endif

		.if defined(mux_c)
		rcall	wait30ms
		ldi	xl, 8			; error code 8: phase c low-side drive broken
		ldi	temp4, mux_c
		rcall	adc_read
		movw	yl, temp1		; save adc value (hopefully non-zero)
		cnfet_on			; drive down this phase (we've established that it was 0v above).
		rcall	adc_read		; fet turn-on will easily beat adc initialization
		cnfet_off
		rcall	hw_error_y_le_temp12
		.endif

		cbi	portd, 6		; sink on ain0 (help to pull down the outputs)
		rcall	wait30ms

		.if defined(mux_a)
		ldi	xl, 9			; error code 9: phase a high-side drive broken
		ldi	temp4, mux_a
		rcall	adc_read
		movw	yl, temp1		; save adc value (hopefully non-zero)
		apfet_on			; drive up this phase.
		rcall	adc_read		; waste time for high side to turn off
		apfet_off
		rcall	hw_error_temp12_le_y
		.endif

		.if defined(mux_b)
		ldi	xl, 10			; error code 10: phase b high-side drive broken
		ldi	temp4, mux_b
		rcall	adc_read
		movw	yl, temp1		; save adc value (hopefully non-zero)
		bpfet_on			; drive up this phase.
		rcall	adc_read		; waste time for high side to turn off
		bpfet_off
		rcall	hw_error_temp12_le_y
		.endif

		.if defined(mux_c)
		ldi	xl, 11			; error code 11: phase c high-side drive broken
		ldi	temp4, mux_c
		rcall	adc_read
		movw	yl, temp1		; save adc value (hopefully non-zero)
		cpfet_on			; drive up this phase.
		rcall	adc_read		; waste time for high side to turn off
		cpfet_off
		rcall	hw_error_temp12_le_y
		.endif

		cbi	ddrd, 6			; restore tristated ain0
		ret

check_sense_low:
		ldi2	yl, yh, max_check_loops
check_sense_low1:
		rcall	adc_read
		; up to 3.5v to account for adc offset or driver pull-up.
		.equ	off_max_adc = 35 * 1024 * o_ground / (50 * (o_power + o_ground))
		sbiwx	temp1, temp2, off_max_adc
		brcs	check_sense_low_ret	; return if pin reads low
		sbiw	yl, 1
		brne	check_sense_low1	; loop until timeout
		rjmp	hw_error
check_sense_low_ret:
		ret

hw_error_temp12_le_y:
		cp	yl, temp1
		cpc	yh, temp2
		brcc	hw_error
		ret

hw_error_y_le_temp12:
		cp	temp1, yl
		cpc	temp2, yh
		brcc	hw_error
		ret

;-- hardware error -------------------------------------------------------
; blink an led or beep xl times to indicate a hardware error.
; beeping is possibly unsafe. the only other option is to stop.
hw_error_eq:
		brne	hw_error_ret
hw_error:
		mov	yl, xl
hw_error1:
		.if defined(red_led)
		red_on
		rcall	wait120ms
		red_off
		.elif defined(blue_led)
		blue_on
		rcall	wait120ms
		blue_off
		.elif defined(green_led)
		grn_on
		rcall	wait120ms
		grn_off
		.else
		rcall	beep_f1			; low frequency is safer
		.endif
		rcall	wait240ms
		dec	yl
		brne	hw_error1
		rcall	wait240ms
		rcall	wait240ms
		set
hw_error_ret:	ret

.endif

;-------------------------------------------------------------------------
; adc value dumping via the uart. expects vt100ish.
.if debug_adc_dump
.set debug_tx = 1
.set adc_read_needed = 1

adc_input_dump:
		ldi	temp4, 27
		rcall	tx_byte
		ldi	temp4, '['
		rcall	tx_byte
		ldi	temp4, '2'
		rcall	tx_byte
		ldi	temp4, 'j'
		rcall	tx_byte

adc_input_dump1:
		ldi	temp2, 5
		rcall	wait1
		ldi	temp4, 27
		rcall	tx_byte
		ldi	temp4, '['
		rcall	tx_byte
		ldi	temp4, 'h'
		rcall	tx_byte

		.if defined(mux_a)
		ldi	temp4, 'a'
		rcall	tx_byte
		ldi	temp4, mux_a
		rcall	adc_read
		rcall	colon_hex_write
		.endif
		.if defined(mux_b)
		ldi	temp4, 'b'
		rcall	tx_byte
		ldi	temp4, mux_b
		rcall	adc_read
		rcall	colon_hex_write
		.endif
		.if defined(mux_c)
		ldi	temp4, 'c'
		rcall	tx_byte
		ldi	temp4, mux_c
		rcall	adc_read
		rcall	colon_hex_write
		.endif
		.if defined(mux_voltage)
		ldi	temp4, '#'
		rcall	tx_byte
		rcall	adc_cell_count
		clr	temp2
		rcall	colon_hex_write
		.endif

		rcall	tx_crlf

		clr	yl
adc_loop:
		ldi	temp4, 'a'
		rcall	tx_byte
		ldi	temp4, 'd'
		rcall	tx_byte
		ldi	temp4, 'c'
		rcall	tx_byte
		mov	temp4, yl
		rcall	tx_hex_nibble

		mov	temp4, yl
		rcall	adc_read
		rcall	colon_hex_write

		inc	yl
		andi	yl, 0xf
		breq	adc_input_dump1
		cpi	yl, 8
		brne	adc_loop
		ldi	yl, 0xe		; jump to band-gap reference (no adc8 - adc13)
		rjmp	adc_loop

		ret
.endif

.if debug_tx
init_debug_tx:
.if !defined(txd) && dir_pd & (1<<1)
.error "cannot use uart tx with this pin configuration"
.endif
	; initialize tx for debugging on boards with free tx pin
		.equ	d_baud_rate = 38400
		.equ	d_ubrr_val = f_cpu / d_baud_rate / 16 - 1
		outi	ubrrh, high(d_ubrr_val), temp1
		outi	ubrrl, low(d_ubrr_val), temp1
;		sbi	ucsra, u2x		; double speed
		sbi	ucsrb, txen
		outi	ucsrc, (1<<ursel)|(1<<ucsz1)|(1<<ucsz0), temp1	; n81
		ret

tx_hex_byte:
		mov	temp4, temp3
		swap	temp4
		rcall	tx_hex_nibble
		mov	temp4, temp3
tx_hex_nibble:
		andi	temp4, 0xf
		ori	temp4, '0'
		cpi	temp4, '9' + 1
		brcs	tx_byte
		subi	temp4, '9' + 1 - 'a'
tx_byte:
		sbis	ucsra, udre
		rjmp	tx_byte
		out	udr, temp4
		ret

tx_crlf:
		ldi	temp4, 13
		rcall	tx_byte
		ldi	temp4, 10
		rjmp	tx_byte

tx_colonhex:
		ldi	temp4, ':'
		rcall	tx_byte
		ldi	temp4, ' '
		rcall	tx_byte
		ldi	temp4, '0'
		rcall	tx_byte
		ldi	temp4, 'x'
		rjmp	tx_byte

colon_hex_write:
		rcall	tx_colonhex
		mov	temp3, temp2
		rcall	tx_hex_byte
		mov	temp3, temp1
		rcall	tx_hex_byte
		rjmp	tx_crlf
.endif

;-- battery cell count ---------------------------------------------------
; assuming a lipo cell will never exceed 4.3v, we can estimate
; the number of cells by dividing the measured voltage by 4.3.
.if defined(mux_voltage) && (debug_adc_dump || (!cell_count && blip_cell_count))
.set adc_read_needed = 1

adc_cell_count:
		ldi	temp4, mux_voltage | (1<<adlar)
		rcall	adc_read
		ldi2	temp3, temp4, 256 * 50 * (o_power + o_ground) / (o_ground * cell_max_dv)
		ldi2	yl, yh, 0x100		; always at least one cell
		rcall	mul_y_12x34
		mov	temp1, yh
		ret
.endif

.if adc_read_needed
;-- adc input ------------------------------------------------------------
; read adc from the mux set in temp4 and return result in temp1:temp2.
;
; the adc clock on the atmega8 needs to run between 50khz and 200khz for
; full 10-bit sampling. at 8mhz or 16mhz, we can use /128 to get 62.5khz
; or 125khz. the datasheet says we can overclock further at the expense
; of least significant bits. however, overclocked initialization appears
; to offset zero when running the adc clock any faster than 500khz. as we
; must turn off the adc to use admux for the comparator while driving the
; motor, the maximum usable speed appears to be 500khz (16mhz/32). this
; results in minimum sample time of 25/500khz == 50microseconds. slowest
; sample time is 100microseconds at 8mhz or 200microseconds at 16mhz.
;
; if avcc is tied to aref, the adc is intended to be used with no
; internal reference enabled (refs0 and refs1 not set). if a capacitor is
; at aref, one of the internal references must be set, or all the
; capacitor will not charge and all adc channels will read 0x3ff. refs1
; can still be set with avcc bridged to aref, since then it just gets
; bridged internally and externally. the internal 2.56v reference (refs0
; and refs1 set) can only be enabled if avcc is not bridged.
adc_read:
		out	sfior, zh		; disable the analog comparator multiplexer
		sbr	temp4, (1<<refs0)	; enable avcc (5.0v) reference
		out	admux, temp4		; set adc channel, avcc reference with cap at aref (should be safe if bridged)
		ldi	temp1, (1<<aden)+(1<<adsc)+(1<<adps2)+(1<<adps1)+(1<<adps0)
		out	adcsra, temp1		; enable the adc, start conversion
		wdr				; will wait 25*128 cycles
adc_wait:	sbic	adcsra, adsc
		rjmp	adc_wait
		in	temp1, adcl
		in	temp2, adch
		out	adcsra, zh		; disable the adc (next enable and sample will take 25 adc cycles)
		ret
.endif

;-----bko-----------------------------------------------------------------
; unlike the normal evaluate_rc, we look here for programming mode (pulses
; above program_rc_puls), unless we have received i2c or uart input.
;
; with pulse width modulation (pwm) input, we have to be careful about
; oscillator drift. if we are running on a board without an external
; crystal/resonator/oscillator, the internal rc oscillator must be used,
; which can drift significantly with temperature and voltage. so, we must
; use some margins while calibrating. the internal rc speeds up when cold,
; causing arming problems if the learned pulse is too low. likewise, the
; internal rc slows down when hot, making it impossible to reach full
; throttle.
evaluate_rc_init:
		.if use_uart
		sbrc	flags1, uart_mode
		rjmp	evaluate_rc_uart
		.endif
		.if use_i2c
		sbrc	flags1, i2c_mode
		rjmp	evaluate_rc_i2c
		.endif
		.if use_icp || use_int0
		cbr	flags1, (1<<eval_rc)
	; check if pulse is in aliased input range and toggle rcp_alias to match
		.if defined(rcp_alias_shift)
		movw	temp1, rx_l		; atomic copy of rc pulse length
		sbiwx	temp1, temp2, min_rc_puls * cpu_mhz
		sbc	temp3, temp3		; all temp3 bits <- carry (set if in aliased range)
		eor	temp3, flags0		; xor against current flags0 value
		sbrc	temp3, rcp_alias	; skip if zero (no change)
		rjmp	puls_scale_alias_toggle	; or toggle rcp_alias and rescale
		.endif
		.if rc_calibration
		sbrc	flags0, no_calibration	; is it safe to calibrate now?
		rjmp	evaluate_rc_puls
	; if input is above program_rc_puls, we try calibrating throttle
		ldi2	yl, yh, puls_high_l	; start with high pulse calibration
		rjmp	rc_prog1
rc_prog0:	rcall	wait240ms		; wait for stick movement to settle
	; collect average of throttle input pulse length
rc_prog1:	rcall	rc_prog_get_rx		; pulse length into temp1:temp2
		movw	temp3, temp1		; save starting pulse length
		wdr
rc_prog2:	mul	zh, zh			; clear 24-bit result registers (0 * 0 -> temp5:temp6)
		clr	temp7
		cpi	yl, low(puls_high_l)	; are we learning the high pulse?
		brne	rc_prog3		; no, maybe the low pulse
		ldi	temp2, 32 * 31/32	; full speed pulse averaging count (slightly below exact)
		cpi2	temp3, temp4, program_rc_puls * cpu_mhz, temp1
		brcc	rc_prog5		; equal to or higher than program_rc_puls - start measuring
		rjmp	evaluate_rc_puls	; lower than program_rc_puls - exit programming
rc_prog3:	lds	temp1, puls_high_l	; if not learning the high pulse, we should stay below it
		cp	temp3, temp1
		lds	temp1, puls_high_h
		cpc	temp4, temp1
rc_prog_brcc_prog1:				; branch trampoline
		brcc	rc_prog1		; restart while pulse not lower than learned high pulse
		ldi	temp2, 32 * 17/16	; stop/reverse pulse (slightly above exact)
		cpi	yl, low(puls_low_l)	; are we learning the low pulse?
		breq	rc_prog5		; yes, start measuring
rc_prog4:	lds	temp1, puls_low_l
		cp	temp3, temp1
		lds	temp1, puls_low_h
		cpc	temp4, temp1
		brcs	rc_prog1		; restart while pulse lower than learned low pulse
		ldi	temp2, 32		; neutral pulse measurement (exact)
rc_prog5:	mov	tcnt2h, temp2		; abuse tcnt2h as pulse counter
rc_prog6:	wdr
		sbrs	flags1, eval_rc		; wait for next pulse
		rjmp	rc_prog6
		cbr	flags1, (1<<eval_rc)
		rcall	rc_prog_get_rx		; pulse length into temp1:temp2
		add	temp5, temp1		; accumulate 24-bit average
		adc	temp6, temp2
		adc	temp7, zh
		sub	temp1, temp3		; subtract the starting pulse from this one
		sbc	temp2, temp4		; to find the drift since the starting pulse
	; check for excessive drift with an emulated signed comparison -
	; add the drift amount to offset the negative side to 0
		adiwx	temp1, temp2, max_drift_puls * cpu_mhz
	; ..then subtract the 2*drift + 1 -- carry will be clear if
	; we drifted outside of the range
		sbiwx	temp1, temp2, 2 * max_drift_puls * cpu_mhz + 1
		brcc	rc_prog0		; wait and start over if input moved
		dec	tcnt2h
		brne	rc_prog6		; loop until average accumulated
		ldi	temp1, 3
		rcall	lsl_temp567		; multiply by 8 (so that 32 loops makes average*256)
		st	y+, temp6		; save the top 16 bits as the result
		st	y+, temp7
	; one beep: high (full speed) pulse received
		rcall	beep_f3
		cpi	yl, low(puls_high_l+2)
		breq	rc_prog_brcc_prog1	; go back to get low pulse
	; two beeps: low (stop/reverse) pulse received
		rcall	wait30ms
		rcall	beep_f3
		cpi	yl, low(puls_low_l+2)
		.if rc_puls_reverse
		breq	rc_prog_brcc_prog1	; go back to get neutral pulse
		.else
		breq	rc_prog_done
		.endif
	; three beeps: neutral pulse received
		rcall	wait30ms
		rcall	beep_f3
rc_prog_done:	rcall	eeprom_write_block
		rjmp	puls_scale		; calculate the new scaling factors
rc_prog_get_rx:
		movw	temp1, rx_l		; atomic copy of rc pulse length
		.if defined(rcp_alias_shift)
		sbrs	flags0, rcp_alias
		ret
		ldi	xl, rcp_alias_shift
rc_prog_get_rx1:
		lsl	temp1
		rol	temp2
		dec	xl
		brne	rc_prog_get_rx1
		.endif
		ret
		.endif
		.endif
	; fall through from evaluate_rc_init
;-----bko-----------------------------------------------------------------
; these routines may clobber temp* and y, but not x.
evaluate_rc:
		.if use_uart
		sbrc	flags1, uart_mode
		rjmp	evaluate_rc_uart
		.endif
		.if use_i2c
		sbrc	flags1, i2c_mode
		rjmp	evaluate_rc_i2c
		.endif
	; fall through to evaluate_rc_puls
;-----bko-----------------------------------------------------------------
.if use_icp || use_int0
evaluate_rc_puls:
		cbr	flags1, (1<<eval_rc)+(1<<reverse)
		.if motor_brake || low_brake
		sts	brake_want, zh
		.endif
		movw	temp1, rx_l		; atomic copy of rc pulse length
		cpi2	temp1, temp2, min_rc_puls * cpu_mhz, temp3
		brcs	puls_length_short	; branch if short pulse
		.if defined(rcp_alias)
		sbrc	flags0, rcp_alias
		rjmp	puls_length_error	; throw away long pulse if alias range expected
		.endif
puls_alias:
		.if low_brake
		lds	yl, low_brake_l
		lds	yh, low_brake_h
		cp	temp1, yl
		cpc	temp2, yh
		brcc	puls_not_low_brake
		ldi	yl, 2
		sts	brake_want, yl		; set desired brake to 2 (low brake)
		rjmp	puls_zero
puls_not_low_brake:
		.endif
		lds	yl, neutral_l
		lds	yh, neutral_h
		sub	temp1, yl		; offset input to neutral
		sbc	temp2, yh
		brcc	puls_plus
		.if rc_puls_reverse
		sbr	flags1, (1<<reverse)
		com	temp2			; negate 16-bit value to get positive duty cycle
		neg	temp1
		sbci	temp2, -1
		lds	temp3, rev_scale_l	; load reverse scaling factor
		lds	temp4, rev_scale_h
		rjmp	puls_not_zero
		.endif
		; fall through to stop/zero in no reverse case
puls_zero_brake:
		.if motor_brake
		ldi	yl, 1
		sts	brake_want, yl		; set desired brake to 1 (neutral brake)
		.endif
puls_zero:	clr	yl
		clr	yh
		rjmp	rc_duty_set
puls_length_short:
		.if defined(rcp_alias_shift)
		; additional length checks for aliased range
		cpi2	temp1, temp2, (min_rc_puls * cpu_mhz) >> rcp_alias_shift, temp3
		brcs	puls_length_error
		cpi2	temp1, temp2, (max_rc_puls * cpu_mhz) >> rcp_alias_shift, temp3
		sbrc	flags0, rcp_alias	; if alias range expected
		brcs	puls_alias
		.endif
puls_length_error:
		sbr	flags0, (1<<rcp_error)
		ret
puls_plus:
		lds	temp3, fwd_scale_l	; load forward scaling factor
		lds	temp4, fwd_scale_h
puls_not_zero:
		.if rcp_deadband
		.if defined(rcp_alias_shift)
		lds	yl, deadband_l		; load dynamic deadband
		lds	yh, deadband_h
		sub	temp1, yl
		sbc	temp2, yh
		.else
		sbiwx	temp1, temp2, rcp_deadband * cpu_mhz
		.endif
		brmi	puls_zero_brake
		.endif
.endif
	; the following is used by all input modes
rc_do_scale:	ldi2	yl, yh, min_duty	; offset result so that 0 is min_duty
		rcall	mul_y_12x34		; scaled result is now in y
		cpi2	yl, yh, max_power, temp1
		brcs	rc_duty_set
		ldi2	yl, yh, max_power
rc_duty_set:	sts	rc_duty_l, yl
		sts	rc_duty_h, yh
		sbrs	flags0, set_duty
		rjmp	rc_no_set_duty
		ldi	temp1, rcp_tot
		mov	rc_timeout, temp1	; short rc_timeout when driving
		rjmp	set_new_duty_l		; skip reload into yl:yh
rc_no_set_duty:	ldi	temp1, 12		; more than 10 needed to arm
		cp	rc_timeout, temp1
		adc	rc_timeout, zh
		ret
;-----bko-----------------------------------------------------------------
.if use_i2c
evaluate_rc_i2c:
		movw	yl, rx_l		; atomic copy of 16-bit input
		cbr	flags1, (1<<eval_rc)
	; load settings from blconfig structure (bl-ctrl v2)
		lds	temp1, blc_bitconfig
		bst	temp1, 0		; bitconfig bit 0: reverse
		bld	flags1, reverse
	; mk sends one or two bytes, if supported, and if low bits are
	; non-zero. we store the first received byte in rx_h, second
	; in rx_l. there are 3 low bits which are stored at the low
	; side of the second byte, so we must shift them to line up with
	; the high byte. the high bits become less significant, if set.
		lsl	yl			; 00000xxxb -> 0000xxx0b
		swap	yl			; 0000xxx0b -> xxx00000b
		adiw	yl, 0			; 16-bit zero-test
		breq	rc_duty_set		; power off
	; scale so that yh == 247 is max_power, to support reaching full
	; power from the highest maxgas setting in mk-tools. bernhard's
	; original version reaches full power at around 245.
		movw	temp1, yl
		ldi2	temp3, temp4, 0x100 * (power_range - min_duty) / 247
		rjmp	rc_do_scale		; the rest of the code is common
.endif
;-----bko-----------------------------------------------------------------
.if use_uart
evaluate_rc_uart:
		mov	yh, rx_h		; copy 8-bit input
		cbr	flags1, (1<<eval_rc)+(1<<reverse)
		ldi	yl, 0
		cpi	yh, 0
		breq	rc_duty_set		; power off
	; scale so that yh == 200 is max_power.
		movw	temp1, yl
		ldi2	temp3, temp4, 0x100 * (power_range - min_duty) / 200
		rjmp	rc_do_scale		; the rest of the code is common
.endif
;-----bko-----------------------------------------------------------------
; if an input alias is defined, shift the values in temp3:temp4 and
; temp1:temp2 to follow.
.if defined(rcp_alias_shift)
puls_scale_alias_shift:
		ldi	yl, rcp_alias_shift
puls_scale_alias_shift1:
		lsr	temp4
		ror	temp3
		lsr	temp2
		ror	temp1
		dec	yl
		brne	puls_scale_alias_shift1
		ret
;-----bko-----------------------------------------------------------------
; toggle rcp_alias and fall through to pulse_scale
puls_scale_alias_toggle:
		ldi	temp1, (1<<rcp_alias)
		eor	flags0, temp1
.endif
;-----bko-----------------------------------------------------------------
; calculate the neutral offset and forward (and reverse) scaling factors
; to line up with the high/low (and neutral) pulse lengths.
puls_scale:
	; if reversible input is enabled, scale from neutral; otherwise,
	; scale from the lowest point.
		.if rc_puls_reverse
		lds	temp1, puls_neutral_l
		lds	temp2, puls_neutral_h
		.else
		lds	temp1, puls_low_l
		lds	temp2, puls_low_h
		.endif
		lds	temp3, puls_high_l
		lds	temp4, puls_high_h
		.if defined(rcp_alias_shift)
		sbrc	flags0, rcp_alias
		rcall	puls_scale_alias_shift
		.endif
	; find the distance to full throttle and fit it to match the
	; distance between full_rc_puls and stop_rc_puls by walking
	; for the lowest 16.16 multiplier that just brings us in range.
		sts	neutral_l, temp1
		sts	neutral_h, temp2
		sub	temp3, temp1
		sbc	temp4, temp2
		rcall	puls_find_multiplicand
		sts	fwd_scale_l, temp1
		sts	fwd_scale_h, temp2

	; if reversible input is enabled, calculate the reverse scaling
	; factor.
		.if rc_puls_reverse
		lds	temp3, puls_neutral_l
		lds	temp4, puls_neutral_h
		lds	temp1, puls_low_l
		lds	temp2, puls_low_h
		.if defined(rcp_alias_shift)
		sbrc	flags0, rcp_alias
		rcall	puls_scale_alias_shift
		.endif
		sub	temp3, temp1
		sbc	temp4, temp2
		rcall	puls_find_multiplicand
		sts	rev_scale_l, temp1
		sts	rev_scale_h, temp2
		.endif

	; if a deadband is enabled and input aliases are supported,
	; calculate the scaled deadband in advance.
		.if rcp_deadband && defined(rcp_alias_shift)
		ldi2	temp1, temp2, rcp_deadband * cpu_mhz
		sbrc	flags0, rcp_alias
		rcall	puls_scale_alias_shift
		sts	deadband_l, temp1
		sts	deadband_h, temp2
		.endif

	; if low brake is enabled, store the position where it starts
	; after subtracting the deadband. this avoids the calculation
	; in the receive path.
		.if low_brake
		lds	temp1, puls_low_l		; lowest calibrated pulse (regardless of rc_puls_reverse)
		lds	temp2, puls_low_h
		ldi2	temp3, temp4, rcp_low_dband * cpu_mhz
		.if defined(rcp_alias_shift)
		sbrc	flags0, rcp_alias
		rcall	puls_scale_alias_shift
		.endif
		sub	temp1, temp3
		sbc	temp2, temp4
		brcc	puls_low_brake_save
		ldi2	temp1, temp2, 0
puls_low_brake_save:
		sts	low_brake_l, temp1
		sts	low_brake_h, temp2
		.endif

		ret
;-----bko-----------------------------------------------------------------
; find the lowest 16.16 multiplicand that brings us to full throttle
; (power_range - min_duty) when multiplied by temp3:temp4.
; the range we are looking for is around 3000 - 10000:
; m = (power_range - min_duty) * 65536 / (1000us * 16mhz)
; if the input range is < 100us at 8mhz, < 50us at 16mhz, we return
; too low a multiplicand (higher won't fit in 16 bits).
; we preload temp1:temp2 with the weakest possible scale based on the
; fact that we can't accept a wider range than max_rc_puls microseconds.
puls_find_multiplicand:
		.if rcp_deadband
		sbi2	temp3, temp4, rcp_deadband * cpu_mhz
		.endif
		ldi2	temp1, temp2, (power_range - min_duty) * 65536 / (max_rc_puls * cpu_mhz)
puls_find1:	adiw	temp1, 1
		wdr
		cpi	temp2, 0xff
		cpc	temp1, temp2
		breq	puls_find_fail		; return if we reached 0xffff
	; start with negative power_range so that 0 is full throttle
		ldi2	yl, yh, min_duty - power_range
		rcall	mul_y_12x34
	; we will always be increasing the result in steps of less than 1,
	; so we can test for just zero rather than a range.
		brne	puls_find1
puls_find_fail:	ret
;-----bko-----------------------------------------------------------------
update_timing:
		cli
		in	temp1, tcnt1l
		in	temp2, tcnt1h
		lds	temp3, tcnt1x
		in	temp4, tifr
		sei
		cpi	temp2, 0x80		; tcnt1x is right when tcnt1h[7] set;
		sbrc	temp4, tov1		; otherwise, if tov1 is/was pending,
		adc	temp3, zh		; increment our copy of tcnt1x.

	; calculate the timing from the last two zero-crossings
		lds	yl, last_tcnt1_l	; last -> y
		lds	yh, last_tcnt1_h
		lds	temp7, last_tcnt1_x
		sts	last_tcnt1_l, temp1
		sts	last_tcnt1_h, temp2
		sts	last_tcnt1_x, temp3
		lds	temp5, l2_tcnt1_l	; last2 -> temp5
		lds	temp6, l2_tcnt1_h
		lds	temp4, l2_tcnt1_x
		sts	l2_tcnt1_l, yl
		sts	l2_tcnt1_h, yh
		sts	l2_tcnt1_x, temp7

	; cancel dc bias by starting our timing from the average of the
	; last two zero-crossings. commutation phases always alternate.
	; next start = (cur(c) - last2(a)) / 2 + last(b)
	; -> start=(c-b+(c-a)/2)/2+b
	;
	;                  (c - a)
	;         (c - b + -------)
	;                     2
	; start = ----------------- + b
	;                 2

		sub	temp1, temp5		; c' = c - a
		sbc	temp2, temp6
		sbc	temp3, temp4

	; limit maximum rpm (fastest timing)
		cpi3	temp1, temp2, temp3, timing_max * cpu_mhz / 2, temp4
		brcc	update_timing1
		ldi3	temp1, temp2, temp3, timing_max * cpu_mhz / 2
		lsr	sys_control_h		; limit by reducing power
		ror	sys_control_l
update_timing1:

	; calculate a hopefully sane duty cycle limit from this timing,
	; to prevent excessive current if high duty is requested at low
	; speed. this is the best we can do without a current sensor.
	; the actual current peak will depend on motor kv and voltage,
	; so this is just an approximation. this is calculated smoothly
	; with a (very slow) software divide only if timing permits.
		cpi2	temp2, temp3, (timing_range3 * cpu_mhz / 2) >> 8, temp4
		ldi2	xl, xh, max_power
		brcs	update_timing4	; fast timing: no duty limit.

		; 24.8-bit fixed-point unsigned divide, inlined with available registers:
		; duty (xl:xh) = max_power * (timing_range3 * cpu_mhz / 2) / period (temp1:temp2:temp3)
		; this takes about one microsecond per loop, but we only take this path
		; when the motor is spinning slowly.

		ldi	xl, byte3(max_power * (timing_range3 * cpu_mhz / 2) / 0x100)
		ldi	xh, 33		; iteration counter
		movw	timing_duty_l, xl
		ldi2	xl, xh, max_power * (timing_range3 * cpu_mhz / 2) / 0x100

		mul	zh, zh		; zero temp5, temp6
		sub	temp4, temp4	; zero temp4, clear carry
		rjmp	fudiv24_ep	; jump with carry clear
fudiv24_loop:
		rol	temp4
		rol	temp5
		rol	temp6
		cp	temp4, temp1	; divide by commutation period
		cpc	temp5, temp2
		cpc	temp6, temp3
		brcs	fudiv24_ep
		sub	temp4, temp1
		sbc	temp5, temp2
		sbc	temp6, temp3
fudiv24_ep:
		rol	xl
		rol	xh
		rol	timing_duty_l
		dec	timing_duty_h
		brne	fudiv24_loop
		com	xl
		com	xh

		cpi2	xl, xh, pwr_max_rpm1, temp4
		brcc	update_timing4
		ldi2	xl, xh, pwr_max_rpm1
update_timing4:	movw	timing_duty_l, xl

		sts	timing_l, temp1		; store timing (120 degrees)
		sts	timing_h, temp2
		sts	timing_x, temp3

		lsr	temp3			; c'>>= 1 (shift to 60 degrees)
		ror	temp2
		ror	temp1

.if defined(dc_bias_cancel)
		lds	temp5, last_tcnt1_l	; restore original c as a'
		lds	temp6, last_tcnt1_h
		lds	temp4, last_tcnt1_x
		sub	temp5, yl		; a'-= b
		sbc	temp6, yh
		sbc	temp4, temp7

		add	temp5, temp1		; a'+= c'
		adc	temp6, temp2
		adc	temp4, temp3
		lsr	temp4			; a'>>= 1
		ror	temp6
		ror	temp5
		add	yl, temp5		; b+= a' -> yl:yh:temp7 become filtered zc time
		adc	yh, temp6
		adc	temp7, temp4
.else
		lds	yl, last_tcnt1_l	; restore original c as a'
		lds	yh, last_tcnt1_h
		lds	temp7, last_tcnt1_x
.endif

		ldi	temp4, (30 - motor_advance) * 256 / 60
		rcall	update_timing_add_degrees
.if timing_offset
		sbiwx	yl, yh, timing_offset * cpu_mhz
		ldi	temp4, byte3(timing_offset * cpu_mhz)
		sbc	temp7, temp4
.endif
		sts	com_time_l, yl		; store start of next commutation
		sts	com_time_h, yh
		sts	com_time_x, temp7

		cpi	temp2, 0x10		; will 240 degrees fit in 15 bits?
		cpc	temp3, zh
		brcs	update_timing_fast
		cbr	flags2, (1<<timing_fast)
		rcall	set_ocr1a_abs_slow	; set timer for start of next commutation
		rjmp	update_timing_out
update_timing_fast:
		sbr	flags2, (1<<timing_fast)
		rcall	set_ocr1a_abs_fast
update_timing_out:

		sbrc	flags1, eval_rc
		rjmp	evaluate_rc		; set new duty either way
	; fall through to set_new_duty
;-----bko-----------------------------------------------------------------
; unlike update_timing above, we try not to clobber xl and xh which are
; used for loop-counting in wait_for_edge.
set_new_duty:	lds	yl, rc_duty_l
		lds	yh, rc_duty_h
set_new_duty_l:	cp	yl, timing_duty_l
		cpc	yh, timing_duty_h
		brcs	set_new_duty10
		movw	yl, timing_duty_l	; limit duty to timing_duty
set_new_duty10:	cp	yl, sys_control_l
		cpc	yh, sys_control_h
		brcs	set_new_duty11
		movw	yl, sys_control_l	; limit duty to sys_control
set_new_duty11:
.if slow_throttle
		; if sys_control is higher than twice the current duty,
		; limit it to that. this means that a steady-state duty
		; cycle can double at any time, but any larger change will
		; be rate-limited.
		ldi2	temp1, temp2, pwr_min_start
		cp	yl, temp1
		cpc	yh, temp2
		brcs	set_new_duty12
		movw	temp1, yl		; temp1:temp2 >= pwr_min_start
set_new_duty12:	lsl	temp1
		rol	temp2
		cp	sys_control_l, temp1
		cpc	sys_control_h, temp2
		brcs	set_new_duty13
		movw	sys_control_l, temp1
set_new_duty13:
.endif
		ldi2	temp1, temp2, max_power
		sub	temp1, yl		; calculate off duty
		sbc	temp2, yh
		breq	set_new_duty_full
		adiw	yl, 0
		breq	set_new_duty_zero
		; not off and not full power
		cbr	flags1, (1<<full_power)
		sbr	flags1, (1<<power_on)
		; at higher pwm frequencies, halve the frequency
		; when starting -- this helps hard drive startup
		.if power_range < 1000 * cpu_mhz / 16
		sbrs	flags1, startup
		rjmp	set_new_duty_set
		lsl	temp1
		rol	temp2
		lsl	yl
		rol	yh
		.endif
set_new_duty_set:
		; when off duty is short, skip complementary pwm; otherwise,
		; compensate the off_duty time to account for the overhead.
	.if cpwm_soft
		set
		ldi	temp4, pwm_on_fast	; short off period: skip complementary pwm
		cpse	temp2, zh
		ldi	temp4, pwm_on_fast_high	; off period >= 0x100
		cpi2	temp1, temp2, cpwm_overhead_high + cpwm_overhead_low, temp3
		brcs	set_new_duty21		; off period < off-to-on cycle count plus interrupt overhead
		clt				; not short off period, unset skip_cpwm
		sbiwx	temp1, temp2, cpwm_overhead_high
	.endif
		ldi	temp4, pwm_on		; off period < 0x100
		cpse	temp2, zh
		ldi	temp4, pwm_on_high	; off period >= 0x100
set_new_duty21:
		com	yl			; save one's complement of both
		com	temp1			; low bytes for up-counting tcnt2
		movw	duty_l, yl		; atomic set new on duty for pwm interrupt
		cli				; critical section (off_duty & flags together)
		movw	off_duty_l, temp1	; set new off duty for pwm interrupt
		sts	pwm_on_ptr, temp4	; set next pwm on interrupt vector
	.if cpwm_soft
		bld	flags2, skip_cpwm	; if to skip complementary pwm
	.endif
		sei
		ret
set_new_duty_full:
		; full power
		sbr	flags1, (1<<full_power)+(1<<power_on)
		rjmp	set_new_duty_set
set_new_duty_zero:
		; power off
		cbr	flags1, (1<<full_power)+(1<<power_on)
		ldi	temp4, pwm_off		; skip the on phase entirely
	.if cpwm_soft
		set				; skip complementary pwm
	.endif
		rjmp	set_new_duty21
;-----bko-----------------------------------------------------------------
; multiply the 24-bit timing in temp1:temp2:temp3 by temp4 and add the top
; 24-bits to yl:yh:temp7.
update_timing_add_degrees:
		mul	temp1, temp4
		add	yl, temp6		; discard byte 1 already
		adc	yh, zh
		adc	temp7, zh
		mul	temp2, temp4
		add	yl, temp5
		adc	yh, temp6
		adc	temp7, zh
		mul	temp3, temp4
		add	yh, temp5
		adc	temp7, temp6
		ret
load_timing:
		lds	temp1, timing_l
		lds	temp2, timing_h
		lds	temp3, timing_x
		lds	yl, com_time_l
		lds	yh, com_time_h
		lds	temp7, com_time_x
		ret
;-----bko-----------------------------------------------------------------
; set zero-crossing timeout to 240 degrees
set_ocr1a_zct_slow:
		rcall	load_timing
		add	yl, temp1
		adc	yh, temp2
		adc	temp7, temp3
		add	yl, temp1
		adc	yh, temp2
		adc	temp7, temp3
		rjmp	set_ocr1a_abs_slow
set_timing_degrees_slow:
		rcall	load_timing
		rcall	update_timing_add_degrees
	; fall through to set_ocr1a_abs_slow
;-----bko-----------------------------------------------------------------
; set oct1_pending until the absolute time specified by yl:yh:temp7 passes.
; returns current tcnt1(l:h:x) value in temp1:temp2:temp3.
;
; tcnt1x may not be updated until many instructions later, even with
; interrupts enabled, because the avr always executes one non-interrupt
; instruction between interrupts, and several other higher-priority
; interrupts may (have) come up. so, we must save tcnt1x and tifr with
; interrupts disabled, then do a correction.
set_ocr1a_abs_slow:
		in	temp4, timsk
		mov	temp5, temp4
		cbr	temp4, (1<<toie1)+(1<<ocie1a)
		out	timsk, temp4		; disable toie1 and ocie1a temporarily
		ldi	temp4, (1<<ocf1a)
		cli
		out	ocr1ah, yh
		out	ocr1al, yl
		out	tifr, temp4		; clear any pending ocf1a interrupt
		in	temp1, tcnt1l
		in	temp2, tcnt1h
		sei
		sbr	flags0, (1<<oct1_pending)
		lds	temp3, tcnt1x
		in	temp4, tifr
		cpi	temp2, 0x80		; tcnt1x is right when tcnt1h[7] set;
		sbrc	temp4, tov1		; otherwise, if tov1 is/was pending,
		adc	temp3, zh		; increment our copy of tcnt1x.
		sub	yl, temp1		; check that time might have already
		sbc	yh, temp2		; passed -- if so, clear pending flag.
		sbc	temp7, temp3
		sts	ocr1ax, temp7
		brpl	set_ocr1a_abs_slow1	; skip set if time has passed
		cbr	flags0, (1<<oct1_pending)
set_ocr1a_abs_slow1:
		out	timsk, temp5		; enable toie1 and ocie1a again
		ret

; variants of the above with a fast path when timing is guaranteed to fit
; within 15 bits.
set_ocr1a_zct:
		sbrs	flags2, timing_fast
		rjmp	set_ocr1a_zct_slow
		lds	temp1, timing_l
		lds	temp2, timing_h
		lds	yl, com_time_l
		lds	yh, com_time_h
		add	yl, temp1
		adc	yh, temp2
		add	yl, temp1
		adc	yh, temp2
		rjmp	set_ocr1a_abs_fast
set_timing_degrees:
		sbrs	flags2, timing_fast
		rjmp	set_timing_degrees_slow
		lds	temp1, timing_l
		lds	temp2, timing_h
		lds	yl, com_time_l
		lds	yh, com_time_h
		mul	temp1, temp4
		add	yl, temp6
		adc	yh, zh
		mul	temp2, temp4
		add	yl, temp5
		adc	yh, temp6
set_ocr1a_abs_fast:
		ldi	temp4, (1<<ocf1a)
		cli
		out	ocr1ah, yh
		out	ocr1al, yl
		out	tifr, temp4		; clear any pending ocf1a interrupt
		in	temp1, tcnt1l
		in	temp2, tcnt1h
		sbr	flags0, (1<<oct1_pending)
		sts	ocr1ax, zh
		sei
		sub	yl, temp1		; check that time might have already
		sbc	yh, temp2		; passed -- if so, clear pending flag.
		brpl	set_ocr1a_abs_fast1	; skip set if time has passed
		cbr	flags0, (1<<oct1_pending)
set_ocr1a_abs_fast1:
		ret
;-----bko-----------------------------------------------------------------
; set oct1_pending until the relative time specified by yl:yh:temp7 passes.
set_ocr1a_rel:	adiw	yl, 7			; compensate for timer increment during in-add-out
		ldi	temp4, (1<<ocf1a)
		cli
		in	temp1, tcnt1l
		in	temp2, tcnt1h
		add	yl, temp1
		adc	yh, temp2
		out	ocr1ah, yh
		out	ocr1al, yl
		out	tifr, temp4		; clear any pending ocf1a interrupt (7 cycles from tcnt1 read)
		sts	ocr1ax, temp7
		sbr	flags0, (1<<oct1_pending)
		sei
		ret
;-----bko-----------------------------------------------------------------
wait_oct1_tot:	sbrc	flags1, eval_rc
		rcall	evaluate_rc
		sbrc	flags0, oct1_pending
		rjmp	wait_oct1_tot		; wait for commutation time
		ret
;-----bko-----------------------------------------------------------------
switch_power_off:
		out	tccr2, zh		; disable pwm
		ldi	temp1, (1<<tov2)
		out	tifr, temp1		; clear pending pwm interrupts
		ldi	zl, low(pwm_wdr)	; stop pwm switching
		all_pfets_off temp1
		all_nfets_off temp1
		.if motor_brake || low_brake
		sts	brake_active, zh	; no active brake
		.endif
		ret
;-----bko-----------------------------------------------------------------
.if boot_jump
boot_loader_test:
		.if use_icp
		sbis	pinb, rcp_in		; skip clear if icp pin high
		.elif use_int0 == 1
		sbis	pind, rcp_in		; skip clear if int0 pin high
		.else
		sbic	pind, rcp_in		; skip clear if int0 pin low (inverted)
		.endif
		sts	rct_boot, zh		; clear rct_count when low
		lds	temp1, rct_boot
		sbrs	temp1, 5 		; wait 32 * 16 * 65536us (~2s) before jumping
boot_ret:	ret
; check for boot loader presence
		ldi	zl, low(boot_start << 1)
		cli				; interrupts depend on zh being 0
		ldi	zh, high(boot_start << 1)
		lpm	temp1, z+
		lpm	temp2, z
		ldi	zh, 0
		sei
		adiw	temp1, 1		; check flash contents for 0xffff or 0x0000
		sbiw	temp1, 2
		brcs	boot_ret		; return if boot loader area is empty
boot_loader_jump:
		cli
		out	ddrb, zh		; tristate pins
		out	ddrc, zh
		out	ddrd, zh
		outi	wdtcr, (1<<wdce)+(1<<wde), temp1
		out	wdtcr, zh		; disable watchdog
		lds	temp1, orig_osccal
		out	osccal, temp1		; restore osccal
		rjmp	boot_start		; jump to boot loader
.endif
;-----bko-----------------------------------------------------------------
.if use_i2c
i2c_init:
		ldi	temp1, i2c_addr + (motor_id << 1)
		.if defined(mk_address_pads)
		sbis	pinb, adr1		; offset motor_id by address pads
		subi	temp1, -1
		sbis	pinb, adr2
		subi	temp1, -2
		.endif
		out	twar, temp1
		outi	twcr, (1<<twie)+(1<<twen)+(1<<twea)+(1<<twint), temp1
		ret
.endif
;-----bko-----------------------------------------------------------------
.if beep_rcp_error
rcp_error_beep:
		rcall	switch_power_off	; brake may have been on
		rcall	wait30ms
		lds	temp4, rcp_beep_count
		subi	temp4, -4
		sts	rcp_beep_count, temp4
		brne	rcp_error_beep_more
		cbr	flags0, (1<<rcp_error)
rcp_error_beep_more:
		ldi	temp2, 18		; short beep pulses to indicate corrupted pwm input
		rjmp	beep_f4_freq
.endif
;-----bko-----------------------------------------------------------------
main:
		clr	r0
		out	sreg, r0		; clear interrupts and flags

	; set up stack
		ldi2	zl, zh, ramend
		out	sph, zh
		out	spl, zl
	; clear ram and all registers
clear_loop:	st	-z, r0
		cpi	zl, sram_start
		cpc	zh, r0
		brne	clear_loop1
		ldi	zl, 30			; start clearing registers
clear_loop1:	cp	zl, r0
		cpc	zh, r0
		brne	clear_loop		; leaves with all registers (r0 through zh) at 0

	; save original osccal and reset cause
		in	temp1, osccal
		sts	orig_osccal, temp1
		in	temp7, mcucsr		; store reset reason in register not used for a while
		out	mcucsr, zh

	; initialize ports
		outi	portb, init_pb, temp1
		outi	ddrb, dir_pb | (motor_debug<<3) | (motor_debug<<4) | (motor_debug<<5), temp1
		outi	portc, init_pc, temp1
		outi	ddrc, dir_pc, temp1
		outi	portd, init_pd, temp1
		outi	ddrd, dir_pd, temp1

	; for pwm-enable drivers, set enable after initializing ports
		.if defined(enable_all_port)
		sbi	enable_all_port, enable_all
		.endif
		.if defined(enable_a_port)
		sbi     enable_a_port, enable_a
		.endif
		.if defined(enable_b_port)
		sbi     enable_b_port, enable_b
		.endif
		.if defined(enable_c_port)
		sbi     enable_c_port, enable_c
		.endif

		.if debug_tx
		rcall	init_debug_tx
		.endif

	; start timers except output pwm
		outi	tccr0, t0clk, temp1	; timer0: beep control, delays
		outi	tccr1b, t1clk, temp1	; timer1: commutation timing, rc pulse measurement
		out	tccr2, zh		; timer2: pwm, stopped

	; enable watchdog (wdton may be set or unset)
		ldi	temp1, (1<<wdce)+(1<<wde)
		out	wdtcr, temp1
		ldi	temp1, (1<<wde)		; fastest option: ~16.3ms timeout
		out	wdtcr, temp1

	; wait for power to settle -- this must be no longer than 64ms
	; (with 64ms delayed start fuses) for i2c v2 protocol detection
		rcall	wait30ms		; running at unadjusted speed(!)

	; debugging hooks
		.if debug_adc_dump
		rcall	adc_input_dump
		.endif

	; read eeprom block to ram
		rcall	eeprom_read_block	; also calls osccal_set
		rcall	eeprom_check_reset

	; early input initialization is required for i2c bl-ctrl v2 detection
	; this serves data from the eeprom, so this is as early as possible.
		.if use_i2c
		rcall	i2c_init
		.endif

	; enable interrupts for early input (i2c)
		sei

	; check hardware (before making any beeps)
		.if check_hardware
		rcall	hardware_check
		.endif

	; check reset cause
		bst	temp7, porf		; power-on reset
		cpse	temp7, zh		; or zero
		brtc	init_no_porf
		rcall	beep_f1			; usual startup beeps
		rcall	beep_f2
		rcall	beep_f3
		rjmp	control_start
init_no_porf:
		sbrs	temp7, borf		; brown-out reset
		rjmp	init_no_borf
		rcall	beep_f3			; "dead cellphone"
		rcall	beep_f1
		sbr	flags0, (1<<no_calibration)
		rjmp	control_start
init_no_borf:
		sbrs	temp7, extrf		; external reset
		rjmp	init_no_extrf
		rcall	beep_f4			; single beep
		rjmp	control_start
init_no_extrf:
		cli				; disable interrupts for terminal reset causes

		sbrs	temp7, wdrf		; watchdog reset
		rjmp	init_no_wdrf
init_wdrf1:	rcall	beep_f1			; "siren"
		rcall	beep_f1
		rcall	beep_f3
		rcall	beep_f3
		rjmp	init_wdrf1		; loop forever
init_no_wdrf:

	; unknown reset cause: beep out all 8 bits
	; sometimes i can cause this by touching the oscillator.
init_bitbeep1:	rcall	wait240ms
		mov	i_temp1, temp7
		ldi	i_temp2, 8
init_bitbeep2:	sbrs	i_temp1, 0
		rcall	beep_f2
		sbrc	i_temp1, 0
		rcall	beep_f4
		rcall	wait120ms
		lsr	i_temp1
		dec	i_temp2
		brne	init_bitbeep2
		rjmp	init_bitbeep1		; loop forever

control_start:

; check cell count
.if blip_cell_count
	.if defined(mux_voltage) && !cell_count
		rcall	adc_cell_count
		cpi	temp1, 5
		brlo	cell_count_good		; detection of >=~5 lipo cells becomes ambiguous based on charge state
		ldi	temp1, 0
cell_count_good:
	.else
		ldi	temp1, cell_count
	.endif
		mov	yl, temp1		; beep clobbers temp1-temp5
		cpi	yl, 0
		breq	cell_blipper1
cell_blipper:
		rcall	wait120ms
		ldi	temp2, 10		; short blip (not too long for this)
		rcall	beep_f4_freq
		dec	yl
		brne	cell_blipper
		rcall	wait120ms
cell_blipper1:

.endif

control_disarm:
	; leds off while disarmed
		blue_off
		grn_off
		red_off

		cbr	flags0, (1<<set_duty)	; we need to count a full rc_timeout for safe arming
		rcall	puls_scale

	; enable timer interrupts (we only do this late to improve beep quality)
		ldi	temp1, (1<<toie1)+(1<<ocie1a)+(1<<toie2)
		out	tifr, temp1		; clear toie1, ocie1a, and toie2 flags
		out	timsk, temp1		; enable t1ovfl_int, t1oca_int, t2ovfl_int

		.if defined(hk_program_card)
	; this program card seems to send data at 1200 baud n81,
	; messages start with 0xdd 0xdd, have 7 bytes of config,
	; and end with 0xde, sent two seconds after power-up or
	; after any jumper change.
		.equ	baud_rate = 1200
		.equ	ubrr_val = f_cpu / baud_rate / 16 - 1
		outi	ubrrh, high(ubrr_val), temp1
		outi	ubrrl, low(ubrr_val), temp1
		sbi	ucsrb, rxen		; do programming card rx by polling
		outi	ucsrc, (1<<ursel)|(1<<ucsz1)|(1<<ucsz0), temp1	; n81
		.endif

	; initialize input sources (i2c and/or rc-puls)
		.if use_uart && !defined(hk_program_card)
		.equ	baud_rate = 38400
		.equ	ubrr_val = f_cpu / baud_rate / 16 - 1
		outi	ubrrh, high(ubrr_val), temp1
		outi	ubrrl, low(ubrr_val), temp1
		sbi	ucsrb, rxen		; we don't actually tx
		outi	ucsrc, (1<<ursel)|(1<<ucsz1)|(1<<ucsz0), temp1	; n81
		in	temp1, udr
		sbi	ucsra, rxc		; clear flag
		sbi	ucsrb, rxcie		; enable reception irq
		.endif
		.if use_i2c
		rcall	i2c_init
		.endif
		.if use_int0 || use_icp
		rcp_int_rising_edge temp1
		rcp_int_enable temp1
		.endif

	; wait for one of the input sources to give arming input

i_rc_puls1:	clr	rc_timeout
		cbr	flags1, (1<<eval_rc)+(1<<i2c_mode)+(1<<uart_mode)
		sts	rct_boot, zh
		sts	rct_beacon, zh
i_rc_puls2:	wdr
		.if defined(hk_program_card)
		.endif
		sbrc	flags1, eval_rc
		rjmp	i_rc_puls_rx
		.if boot_jump
		rcall	boot_loader_test
		.endif
		.if beacon
		lds	temp1, rct_beacon
		cpi	temp1, 120		; beep every 120 * 16 * 65536us (~8s)
		brne	i_rc_puls2
		ldi	temp1, 60
		sts	rct_beacon, temp1	; double rate after the first beep
		rcall	beep_f3			; beacon
		.endif
		rjmp	i_rc_puls2
i_rc_puls_rx:	rcall	evaluate_rc_init
		lds	yl, rc_duty_l
		lds	yh, rc_duty_h
		adiw	yl, 0			; test for zero
		brne	i_rc_puls1
		ldi	temp1, 10		; wait for this count of receiving power off
		cp	rc_timeout, temp1
		brlo	i_rc_puls2
		.if use_i2c
		sbrs	flags1, i2c_mode
		out	twcr, zh		; turn off i2c and interrupt
		.endif
		.if use_uart
		sbrs	flags1, uart_mode
		cbi	ucsrb, rxen		; turn off receiver
		.endif
		.if use_int0 || use_icp
		mov	temp1, flags1
		andi	temp1, (1<<i2c_mode)+(1<<uart_mode)
		breq	i_rc_puls3
		rcp_int_disable temp1		; turn off rc pulse interrupt
i_rc_puls3:
		.endif

		rcall	beep_f4			; signal: rcpuls ready
		rcall	beep_f4
		rcall	beep_f4
		cbr     flags0, (1<<rcp_error)

	; fall through to restart_control
;-----bko-----------------------------------------------------------------
restart_control:
		rcall	switch_power_off	; disables pwm timer, turns off all fets
		cbr	flags0, (1<<set_duty)	; do not yet set duty on input
		.if beacon_idle
		sts	idle_beacon, zh
		.endif
		grn_on				; green on while armed and idle or braking
		red_off
		blue_off
wait_for_power_on_init:
		sts	rct_boot, zh
		sts	rct_beacon, zh

		.if motor_brake || low_brake
		lds	temp3, brake_want
		lds	temp4, brake_active
		cp	temp3, temp4
		breq	wait_for_power_on

		rcall	switch_power_off	; disable any active brake
		sts	brake_active, temp3	; set new brake_active to brake_want

		cpi	temp3, 1		; neutral brake
		brne	set_brake1
		ldi	yl, low(1 << brake_speed)
		sts	brake_sub, yl
		ldi2	yl, yh, brake_power
		rjmp	set_brake_duty

set_brake1:	cpi	temp3, 2		; thumb brake
		brne	wait_for_power_on
		ldi	yl, low(1 << low_brake_speed)
		sts	brake_sub, yl
		ldi2	yl, yh, low_brake_power

set_brake_duty:	ldi2	temp1, temp2, max_power
		sub	temp1, yl		; calculate off duty
		sbc	temp2, yh
		rcall	set_new_duty_set
		ldi	zl, low(pwm_brake_off)	; enable pwm brake mode
		clr	tcnt2h
		clr	sys_control_l		; abused as duty update divisor
		outi	tccr2, t2clk, temp1	; enable pwm, cleared later by switch_power_off
		.endif

wait_for_power_on:
		wdr
		sbrc	flags1, eval_rc
		rjmp	wait_for_power_rx
		.if beep_rcp_error
		sbrc	flags0, rcp_error	; check if we've seen bad pwm edges
		rcall	rcp_error_beep
		.endif
		.if beacon_idle
		lds	temp1, idle_beacon
		cpi	temp1, 240		; beep after ~4 minutes
		brcs	no_idle_beep
		ldi	temp1, 238
		sts	idle_beacon, temp1
		rcall	switch_power_off	; brake may have been on
		rcall	wait30ms
		rcall	beep_f4
no_idle_beep:
		.endif
		tst	rc_timeout
		brne	wait_for_power_on	; tight loop unless rc_timeout is zero
		.if boot_jump
		rcall	boot_loader_test
		.endif
		lds	temp1, rct_beacon
		cpi	temp1, 30		; disarm after ~2 seconds of no signal
		brne	wait_for_power_on
		rcall	switch_power_off	; brake may have been on
		rcall	wait30ms
		rcall	beep_f3			; play beeps for signal lost, disarming
		rcall	beep_f2
		rjmp	control_disarm		; do not start motor until neutral signal received once again

wait_for_power_rx:
		.if use_i2c
		sbrc	flags0, eeprom_reset
		rcall	eeprom_reset_block
		sbrc	flags0, eeprom_write
		rcall	eeprom_write_block
		.endif
		rcall	evaluate_rc		; only get rc_duty, don't set duty
		tst	rc_timeout		; if not a valid signal, loop
		breq	wait_for_power_on	; while increasing boot/beacon timers
		adiw	yl, 0
		brne	start_from_running	; if power requested, start; otherwise,
		rjmp	wait_for_power_on_init	; loop while resetting boot/beacon timers

start_from_running:
		rcall	switch_power_off
		comp_init temp1			; init comparator
		red_off
		grn_off
		blue_on

		ldi2	yl, yh, pwr_min_start	; start with limited power to reduce the chance that we
		movw	sys_control_l, yl	; align to a timing harmonic

		sbr	flags0, (1<<set_duty)
		; set startup flag and call update_timing which will set
		; last_tcnt1 and set the duty (limited by startup) and
		; set power_on.
		rcall	wait_timeout_init
		sts	start_delay, zh
		sts	start_modulate, zh
		sts	start_fail, zh
		ldi	temp1, rcp_tot		; start with a short timeout to stop quickly
		mov	rc_timeout, temp1	; if we see no further pulses after the first.
		ldi	temp1, 6		; do not enable fets during first cycle to
		sts	powerskip, temp1	; see if motor is running, and align to it.
		ldi	temp1, enough_goodies	; if we can follow without a timeout, do not
		sts	goodies, temp1		; continue in startup mode (long zc filtering).
		outi	tccr2, t2clk, temp1	; enable pwm (zl has been set to pwm_wdr)

;-----bko-----------------------------------------------------------------
; *** commutation utilities ***

.macro com1com2
		; bp off, ap on
		set_comp_phase_b temp1
		commutate_b_off
		sbrc	flags1, power_on
		commutate_a_on
.endmacro

.macro com2com1
		; bp on, ap off
		set_comp_phase_a temp1
		commutate_a_off
		sbrc	flags1, power_on
		commutate_b_on
.endmacro

.macro com2com3
		; cn off, bn on
		set_comp_phase_c temp1
		cli
		cbr	flags2, all_fets
		sbr	flags2, (1<<b_fet)
		pwm_focus_c_off
		pwm_c_clear
		pwm_b_copy
		pwm_focus_b_on
		sei
.endmacro

.macro com3com2
		; cn on, bn off
		set_comp_phase_b temp1
		cli
		cbr	flags2, all_fets
		sbr	flags2, (1<<c_fet)
		pwm_focus_b_off
		pwm_b_clear
		pwm_c_copy
		pwm_focus_c_on
		sei
.endmacro

.macro com3com4
		; ap off, cp on
		set_comp_phase_a temp1
		commutate_a_off
		sbrc	flags1, power_on
		commutate_c_on
.endmacro

.macro com4com3
		; ap on, cp off
		set_comp_phase_c temp1
		commutate_c_off
		sbrc	flags1, power_on
		commutate_a_on
.endmacro

.macro com4com5
		; bn off, an on
		set_comp_phase_b temp1
		cli
		cbr	flags2, all_fets
		sbr	flags2, (1<<a_fet)
		pwm_focus_b_off
		pwm_b_clear
		pwm_a_copy
		pwm_focus_a_on
		sei
.endmacro

.macro com5com4
		; bn on, an off
		set_comp_phase_a temp1
		cli
		cbr	flags2, all_fets
		sbr	flags2, (1<<b_fet)
		pwm_focus_a_off
		pwm_a_clear
		pwm_b_copy
		pwm_focus_b_on
		sei
.endmacro

.macro com5com6
		; cp off, bp on
		set_comp_phase_c temp1
		commutate_c_off
		sbrc	flags1, power_on
		commutate_b_on
.endmacro

.macro com6com5
		; cp on, bp off
		set_comp_phase_b temp1
		commutate_b_off
		sbrc	flags1, power_on
		commutate_c_on
.endmacro

.macro com6com1
		; an off, cn on
		set_comp_phase_a temp1
		cli
		cbr	flags2, all_fets
		sbr	flags2, (1<<c_fet)
		pwm_focus_a_off
		pwm_a_clear
		pwm_c_copy
		pwm_focus_c_on
		sei
.endmacro

.macro com1com6
		; an on, cn off
		set_comp_phase_c temp1
		cli
		cbr	flags2, all_fets
		sbr	flags2, (1<<a_fet)
		pwm_focus_c_off
		pwm_c_clear
		pwm_a_copy
		pwm_focus_a_on
		sei
.endmacro

;-----bko-----------------------------------------------------------------
; **** running control loop ****

run1:		.if motor_reverse
		sbrs	flags1, reverse
		.else
		sbrc	flags1, reverse
		.endif
		rjmp	run_reverse

run_forward:
		rcall	wait_for_high
		com1com2
		sync_off
		rcall	wait_for_low
		com2com3
		rcall	wait_for_high
		com3com4
		rcall	wait_for_low
		com4com5
		sync_on
		rcall	wait_for_high
		com5com6
		rcall	wait_for_low
		com6com1
		rjmp	run6

run_reverse:
		rcall	wait_for_low
		com1com6
		sync_on
		rcall	wait_for_high
		com6com5
		rcall	wait_for_low
		com5com4
		rcall	wait_for_high
		com4com3
		sync_off
		rcall	wait_for_low
		com3com2
		rcall	wait_for_high
		com2com1

run6:
		.if motor_brake || low_brake
		lds	temp1, brake_want
		cpse	temp1, zh
		rjmp	run_to_brake
		.endif
		lds	temp1, goodies
		.if !motor_brake
		; if last commutation timed out and power is off, return to restart_control
		cpi	temp1, 0
		sbrs	flags1, power_on
		breq	run_to_brake
		.endif
		movw	yl, sys_control_l	; each time timing_max is hit, sys_control is lsr'd
		adiw	yl, 0			; if zero, try starting over (with powerskipping)
		breq	restart_run
		cpi	temp1, enough_goodies
		brcc	run6_2
		inc	temp1
		sts	goodies, temp1
		; build up sys_control to pwr_max_start in steps.
		adiwx	yl, yh, (power_range + 47) / 48
		ldi2	temp1, temp2, pwr_max_start
		; if we've been trying to start for a while,
		; modulate power to reduce heating.
		lds	temp3, start_fail
		lds	temp4, start_modulate
		subi	temp4, -start_mod_inc
		sts	start_modulate, temp4
		brne	run6_1
		; if we've been trying for a long while, give up.
		subi	temp3, -start_fail_inc
		breq	start_failed
		sts	start_fail, temp3
run6_1:		; allow first loop at full power, then modulate.
		cpi	temp3, start_fail_inc
		brcs	run6_3
		cpi	temp4, start_mod_limit
		brcs	run6_3
		ldi2	temp1, temp2, pwr_cool_start
		rjmp	run6_3

run6_2:
		cbr	flags1, (1<<startup)
		sts	start_fail, zh
		sts	start_modulate, zh
		red_off
		; build up sys_control to max_power in steps.
		; if slow_throttle is disabled, this only limits
		; initial start ramp-up; once running, sys_control
		; will stay at max_power unless timing is lost.
		adiwx	yl, yh, (power_range + 31) / 32
		ldi2	temp1, temp2, max_power
run6_3:		cp	yl, temp1
		cpc	yh, temp2
		brcs	run6_4
		movw	sys_control_l, temp1
		rjmp	run1
run6_4:		movw	sys_control_l, yl
		rjmp	run1

run_to_brake:	rjmp	restart_control
restart_run:	rjmp	start_from_running

;-----bko-----------------------------------------------------------------
; if we were unable to start for a long time, just sit and beep unless
; input goes back to no power. this might help us get found if crashed.
start_failed:
		rcall	switch_power_off
		cbr	flags0, (1<<set_duty)
start_fail_beep:
		rcall	wait240ms
		rcall	wait240ms
		rcall	beep_f4			; "start failed" beacon
		rcall	evaluate_rc		; returns rc_duty when !set_duty
		adiw	yl, 0			; test for zero
		brne	start_fail_beep
		rjmp	control_disarm

;-----bko-----------------------------------------------------------------
demag_timeout:
		ldi	zl, low(pwm_wdr)	; stop pwm switching
		; interrupts will not turn on any fets now
		.if comp_pwm && cpwm_soft
		; turn off complementary pwm if it was on,
		; but leave on the high side commutation fet.
		sbrc	flags2, a_fet
		pwm_comp_a_off
		sbrc	flags2, b_fet
		pwm_comp_b_off
		sbrc	flags2, c_fet
		pwm_comp_c_off
		.elif comp_pwm && !cpwm_soft
		sbrc	flags2, a_fet
		pwm_focus_a_off
		sbrc	flags2, b_fet
		pwm_focus_b_off
		sbrc	flags2, c_fet
		pwm_focus_c_off
		.endif
		pwm_all_off temp1
		red_on
		; skip power for the next commutation. note that this
		; won't decrease a non-zero powerskip because demag
		; checking is skipped when powerskip is non-zero.
		ldi	temp1, 1
		sts	powerskip, temp1
		rjmp	wait_commutation
;-----bko-----------------------------------------------------------------
wait_timeout:
		sbrc	flags1, startup
		rjmp	wait_timeout_start
		cpi	xh, zc_check_fast
		brcs	wait_timeout_run
		ldi	xh, zc_check_fast - 1	; limit back-tracking
		cp	xl, xh
		brcc	wait_timeout1
		mov	xl, xh			; clip current distance from crossing
wait_timeout1:	rcall	set_ocr1a_zct		; set zero-crossing timeout
		rjmp	wait_for_edge2
wait_timeout_run:
		red_on				; turn on red led
wait_timeout_start:
		sts	goodies, zh		; clear good commutation count
		lds	temp4, start_delay
		subi	temp4, -start_delay_inc	; increase start (blanking) delay,
		sbrc	flags1, startup		; unless we were running
		sts	start_delay, temp4
wait_timeout_init:
		sbr	flags1, (1<<startup)	; leave running mode
		rjmp	wait_commutation	; update timing and duty.
;-----bko-----------------------------------------------------------------
wait_for_low:	cbr	flags1, (1<<aco_edge_high)
		rjmp	wait_for_edge
;-----bko-----------------------------------------------------------------
wait_for_high:	sbr	flags1, (1<<aco_edge_high)
;-----bko-----------------------------------------------------------------
; here we wait for the zero-crossing on the undriven phase to synchronize
; with the motor timing. the voltage of the undriven phase should cross
; the average of all three phases at half of the way into the 60-degree
; commutation period.
;
; the voltage on the undriven phase is affected by noise from pwm (mutual
; inductance) and also the demagnetization from the previous commutation
; step. demagnetization time is proportional to motor current, and in
; extreme cases, may take more than 30 degrees to complete. to avoid
; sensing erroneous early zero-crossings in this case and losing motor
; synchronization, we check that demagnetization has finished after the
; minimum blanking period. if we do not see it by the maximum blanking
; period (about 30 degrees since we commutated last), we turn off power
; and continue as if the zc had occurred. pwm is enabled again after the
; next commutation step.
;
; normally, we wait for the blanking window to pass, look for the
; comparator to swing as the sign of the zero crossing, wait for the
; timing delay, and then commutate.
;
; simulations show that the demagnetization period shows up on the phase
; being monitored by the comparator with no pwm-induced noise. as such,
; we do not need any filtering. however, it may not show up immediately
; due to filtering capacitors, hence the initial blind minimum blanking
; period.
;
; special case: powerskipping during start. the idea here is to learn the
; timing of a possibly-spinning motor while not driving it, which would
; induce demagnetization and pwm noise that we cannot ignore until we
; know the timing. we use twice the timeout that would otherwise bring
; zc check count to 0xff. a motor spinning twice the speed or slower will
; fall through to regular startup with zc check count at 0xff. this lets
; us start from braking, rc timeout, or power-up without misaligning.
;
wait_for_edge:
		lds	temp1, powerskip	; are we trying to track a maybe running motor?
		subi	temp1, 1
		brcs	wait_pwm_enable
		sts	powerskip, temp1
		sbrs	flags1, startup
		rjmp	wait_for_edge0
		ldi	yl, byte1(0xff * 0x100)	; timing is 120 degrees, so wait for
		ldi	yh, byte2(0xff * 0x100)	; what would be 0xff at 60 degrees
		mov	temp7, zh
		rcall	set_ocr1a_rel
		ldi	xl, zc_check_min	; there shouldn't be much noise with no power
		rjmp	wait_for_edge1
wait_pwm_enable:
		cpi	zl, low(pwm_wdr)
		brne	wait_pwm_running
		ldi	zl, low(pwm_off)	; re-enable pwm if disabled for powerskip or sync loss avoidance
		red_off				; wait_timeout would have happened if motor not spinning during powerskip
wait_pwm_running:
		sbrc	flags1, startup
		rjmp	wait_startup
		ldi	temp4, 13 * 256 / 120
		rcall	set_timing_degrees
		rcall	wait_oct1_tot		; wait for the minimum blanking period

		ldi	temp4, 42 * 256 / 120
		rcall	set_timing_degrees	; set timeout for maximum blanking period
wait_for_demag:
		sbrs	flags0, oct1_pending
		rjmp	demag_timeout
		sbrc	flags1, eval_rc
		rcall	evaluate_rc
		in	temp3, acsr
		eor	temp3, flags1
		.if high_side_pwm
		sbrs	temp3, aco		; check for opposite level (demagnetization)
		.else
		sbrc	temp3, aco		; check for opposite level (demagnetization)
		.endif
		rjmp	wait_for_demag
wait_for_edge0:
		lds	xl, timing_h		; copy high and extended byte
		lds	xh, timing_x		; to calculate the zc check count
		lsr	xh			; quarter to obtain timing / 1024
		ror	xl
		lsr	xh
		ror	xl
		cpi	xl, zc_check_min
		cpc	xh, zh
		brcs	wait_for_edge_fast_min
		breq	wait_for_edge_fast
.if zc_check_max < 256
		cpi	xl, zc_check_max
.else
		cpi	xl, 255
.endif
		cpc	xh, zh
		brcs	wait_for_edge_below_max
		ldi	xl, zc_check_max	; limit to zc_check_max
wait_for_edge_below_max:
		cpi	xl, zc_check_fast	; for faster timing, set normal zc timeout
		brcs	wait_for_edge_fast
		ldi	temp4, 24 * 256 / 120	; with slower (longer) timing, set timer
		rcall	set_timing_degrees	; for limited backtracing
		rjmp	wait_for_edge1
wait_for_edge_fast_min:
		ldi	xl, zc_check_min
wait_for_edge_fast:
		rcall	set_ocr1a_zct		; set zero-crossing timeout

wait_for_edge1:	mov	xh, xl
wait_for_edge2:	sbrs	flags0, oct1_pending
		rjmp	wait_timeout
		sbrc	flags1, eval_rc
		rcall	evaluate_rc
		in	temp3, acsr
.if 0
		; visualize comparator output on the flag pin
		sbrc	temp3, aco
		flag_on
		nop
		flag_off
		sbrs	temp3, aco
		flag_on
.endif
		eor	temp3, flags1
		.if high_side_pwm
		sbrs	temp3, aco
		.else
		sbrc	temp3, aco
		.endif
		rjmp	wait_for_edge3
		cp	xl, xh			; not yet crossed
		adc	xl, zh			; increment if not at zc_filter
		rjmp	wait_for_edge2
wait_for_edge3:	dec	xl			; zero-cross has happened
		brne	wait_for_edge2		; check again unless temp1 is zero

wait_commutation:
		flag_on
		rcall	update_timing
		sbrs	flags1, startup
		rcall	wait_oct1_tot
		flag_off
		lds	temp1, powerskip
		cpse	temp1, zh
		cbr	flags1, (1<<power_on)	; disable power when powerskipping
		cpse	rc_timeout, zh
		ret
		pop	temp1			; throw away return address
		pop	temp1
		rjmp	restart_control		; restart control immediately on rc timeout

; when starting, we have no idea what sort of motor may be connected,
; or how much time it will take for the previous commutation current
; to stop flowing. if the motor is not spinning and the sensing while
; powerskipping fails, we apply power and check for back-emf after an
; increasing delay, building up further if the motor remains stalled.
; this will stretch out short commutations caused by the comparator
; sitting low or high when inputs are below the minimum offset.
wait_startup:
		ldi3	yl, yh, temp4, start_delay_us * cpu_mhz
		mov	temp7, temp4
		lds	temp1, goodies
		cpi	temp1, 2		; after some good commutations,
		brcc	wait_startup1		; skip additional delay injection
		lds	temp4, start_delay
		ldi3	temp1, temp2, temp3, start_dstep_us * cpu_mhz * 0x100
		rcall	update_timing_add_degrees	; add temp4 (start_delay) start_dsteps of wait
wait_startup1:	rcall	set_ocr1a_rel
		rcall	wait_oct1_tot
		ldi3	yl, yh, temp4, timeout_start * cpu_mhz
		mov	temp7, temp4
		rcall	set_ocr1a_rel
; powered startup: use a fixed (long) zc check count until goodies reaches
; enough_goodies and the startup flag is cleared.
		ldi	xl, 0xff * cpu_mhz / 16
		rjmp	wait_for_edge1

;-----bko-----------------------------------------------------------------
; init after reset

; the reset vector points here, right at the end of the main program.
; this nop-sleds to the boot loader if the program flash was incomplete.
reset:
		rjmp	main

.if boot_loader
.include "boot.inc"
.endif
